// REQUIRES OpenGL and GLU
/*
 * This code was created by Jeff Molofee '99
 * (ported to Linux/SDL by Ti Leggett '01)
 *
 * If you've found this code useful, please let me know.
 *
 * Visit Jeff at http://nehe.gamedev.net/
 *
 * or for port-specific comments, questions, bugreports etc.
 * email to leggett@eecs.tulane.edu
 */

include "SDL/SDL";

include "std/io/faio";
include "flx_faio_sdl";

open C_hack;
open Carray;
open MixedInt;

/* screen width, height, and bit depth */
val SCREEN_WIDTH  = 640;
val SCREEN_HEIGHT = 480;
val SCREEN_BPP = 16;
macro val NUM = 50;

/* function to reset our viewport after a window resize */
proc resizeWindow( wwidth : int, hheight :int)
{
  var height = hheight;
  var width = wwidth;

  /* Protect against a divide by zero */
  if height == 0 do height = 1; done;
  var ratio = double_of width / double_of height;

  block_sdl_events event_lock;
  /* Setup our viewport. */
  glViewport( 0, 0, width, height );

  /* change to the projection matrix and set our viewing volume. */
  glMatrixMode( GL_PROJECTION );
  glLoadIdentity( );

  /* Set our perspective */
  gluPerspective( 45.0, ratio, 0.1, 100.0 );

  /* Make sure we're chaning the model view and not the projection */
  glMatrixMode( GL_MODELVIEW );

  /* Reset The View */
  glLoadIdentity( );
  unblock_sdl_events event_lock;
}

/* function to load in bitmap as a GL texture */
proc LoadGLTextures( )
{
  /* Create storage space for the texture */
  var TextureImage = SDL_LoadBMP(c"demos/sdl/textures/nehe.bmp");
  if isNULL TextureImage do
    print "Can't load texture file demos/sdl/textures/nehe.bmp";
    Quit 1;
  done;

  /* Create The Texture */
    glGenTextures( 1, texture );

  /* Typical Texture Generation Using Data From The Bitmap */
  glBindTexture( GL_TEXTURE_2D, texture.[0] );

    /* Generate The Texture */
  glTexImage2D( GL_TEXTURE_2D, 0, 3, (*TextureImage).w,
    (*TextureImage).h, 0, GL_RGB,
    GL_UNSIGNED_BYTE, (*TextureImage).pixels
  );

  /* Linear Filtering */
    glTexParameteri( GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR );
    glTexParameteri( GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR );

  /* Free up any memory we may have used */
    SDL_FreeSurface( TextureImage );
}

var twinkle = false;

/* Define the star structure */
struct star
{
  /* Stars Color */
  r : int;
  g : int;
  b : int;
  dist : float;  /* Stars Distance From Center */
  angle : float; /* Stars Current Angle */
};

var stars : star ^ NUM; /* Make an array of size 'NUM' of stars */

var zoom = -15.0f; /* Viewing Distance Away From Stars */
var tilt = 90.0f;  /* Tilt The View */

/* general OpenGL initialization function */
proc initGL()
{
  /* Enable Texture Mapping ( NEW ) */
  glEnable( GL_TEXTURE_2D );

  /* Enable smooth shading */
  glShadeModel( GL_SMOOTH );

  /* Set the background black */
  glClearColor( 0.0f, 0.0f, 0.0f, 0.0f );

  /* Depth buffer setup */
  glClearDepth( 1.0 );

  /* Really Nice Perspective Calculations */
  glHint( GL_PERSPECTIVE_CORRECTION_HINT, GL_NICEST );

  /* Blending Function For Translucency Based On Source Alpha Value  */
  glBlendFunc( GL_SRC_ALPHA, GL_ONE );

  /* Enable Blending */
  glEnable( GL_BLEND );

  /* Create A Loop That Goes Through All The Stars */
  var i = 0; while i< NUM do
      /* Start All The Stars At Angle Zero */
      stars.[i].angle = 0.0f;

      /* Calculate Distance From The Center */
      stars.[i].dist = ( float_of i / float_of NUM ) * 5.0f;
      /* Give star.[i] A Random Red Intensity */
      stars.[i].r = Cstdlib::rand( ) % 256;
      /* Give star.[i] A Random Green Intensity */
      stars.[i].g = Cstdlib::rand( ) % 256;
      /* Give star.[i] A Random Blue Intensity */
      stars.[i].b = Cstdlib::rand( ) % 256;
    ++i;
  done;
}

/* These are to calculate our fps */
var T0     = 0u;
var Frames = 0;
var spin = 0.0f;

proc rotate()
{
  var i : int;
  for i in 0 upto NUM - 1 do
    /* Used To Spin The Stars */
    spin += 0.01f;
    /* Changes The Angle Of A Star */
    stars.[i].angle = stars.[i].angle + float_of i / float_of NUM;
    /* Changes The Distance Of A Star */
    stars.[i].dist = stars.[i].dist - 0.01f;

    /* Is The Star In The Middle Yet */
    if stars.[i].dist < 0.0f do
      /* Move The Star 5 Units From The Center */
      stars.[i].dist = stars.[i].dist + 5.0f;
      /* Give It A New Red Value */
      stars.[i].r = Cstdlib::rand( ) % 256;
      /* Give It A New Green Value */
      stars.[i].g = Cstdlib::rand( ) % 256;
      /* Give It A New Blue Value */
      stars.[i].b = Cstdlib::rand( ) % 256;
    done;
  done;
}

var filter = 0;

var f_texture : uint ^ 3; /* Storage For 3 Textures ( NEW ) */
var texture : carray[uint] = carray f_texture;


/* Here goes our drawing code */
proc drawGLScene(drawing:1->0)
{
  block_sdl_events event_lock;
  drawing();
  unblock_sdl_events event_lock;

  /* Gather our frames per second */
  Frames++;
  {
    var t = SDL_GetTicks();
    if t - T0 >= 5000 do
      val seconds = double_of (t - T0) / 1000.0;
      val fps = double_of Frames / seconds;
      print Frames; print " frames in "; print seconds;
      print " seconds = "; print fps; print " FPS"; endl;
      T0 = t;
      Frames = 0;
    done;
  };
  rotate();
}

/* whether or not the window is active */
var isActive = true;

if SDL_Init(SDL_INIT_AUDIO \| SDL_INIT_VIDEO) < 0 do
  print "Unable to init SDL"; endl;
  System::exit(1);
done;

var event_lock = SDL_CreateMutex();


proc Quit(n:int)
{
  SDL_Quit;
  System::exit 0;
}

/* Fetch the video info */
var videoInfo = SDL_GetVideoInfo();

if isNULL videoInfo do
  print "Video query failed"; endl;
  Quit 1;
done;

/* the flags to pass to SDL_SetVideoMode */
var
  videoFlags  = SDL_OPENGL;          /* Enable OpenGL in SDL */
  videoFlags |= cast[uint] SDL_GL_DOUBLEBUFFER; /* Enable double buffering */
  videoFlags |= SDL_HWPALETTE;       /* Store the palette in hardware */
  videoFlags |= SDL_RESIZABLE;       /* Enable window resizing */

/* This checks to see if surfaces can be stored in memory */
if (*videoInfo).hw_available != 0 do
    videoFlags |= SDL_HWSURFACE;
else
    videoFlags |= SDL_SWSURFACE;
done;

/* This checks if hardware blits can be done */
if (*videoInfo).blit_hw != 0 do
    videoFlags |= SDL_HWACCEL;
done;

/* Sets up OpenGL double buffering */
ignore$ SDL_GL_SetAttribute( cast[SDL_GLattr] SDL_GL_DOUBLEBUFFER, 1 );

/* get a SDL surface */
var surface = SDL_SetVideoMode
  (SCREEN_WIDTH, SCREEN_HEIGHT, SCREEN_BPP, videoFlags )
;

/* Verify there is a surface */
if isNULL surface do
  print "Video mode set failed"; endl;
  Quit 1;
done;

/* initialize OpenGL */
initGL();
LoadGLTextures();

/* resize the initial window */
resizeWindow( SCREEN_WIDTH, SCREEN_HEIGHT );

// SDL Event dispatcher
proc handle_active (e:SDL_ActiveEvent)
{
   isActive = e.gain != 0;
}

proc handle_resize(e:SDL_ResizeEvent)
{
  block_sdl_events event_lock;
  surface = SDL_SetVideoMode(
    e.w,
    e.h,
    16, videoFlags
  );
  if isNULL surface do
    print "Could not get a surface after resize"; endl;
  done;
  resizeWindow( e.w, e.h );
  unblock_sdl_events event_lock;
}

/* function to handle key press events */
proc handle_key( keysym : SDL_keysym)
{
  match keysym.sym with
  | ?k when k == SDLK_ESCAPE => { Quit 0; }
  | ?k when k ==  SDLK_F1 =>
        {
        block_sdl_events event_lock;
        ignore$ SDL_WM_ToggleFullScreen( surface );
        unblock_sdl_events event_lock;
      }
  | ?k when k == SDLK_t =>
    {
      twinkle = not twinkle;
    }
  | ?k when k == SDLK_PAGEUP =>
    {
        zoom = zoom - 0.2f;
    }

  | ?k when k == SDLK_PAGEDOWN =>
    {
        zoom = zoom + 0.2f;
    }

  | ?k when k == SDLK_UP =>
    {
      tilt = tilt - 0.5f;
    }

  | ?k when k == SDLK_DOWN =>
    {
      tilt = tilt + 0.5f;
    }

  | _ => {}
  endmatch;
}

/* draw the scene */
proc draw(drawing: 1->0) {
  if isActive call drawGLScene( drawing );
}

proc keychan(x:schannel[SDL_keysym])
{
  while true do
    handle_key$ read x;
  done;
}

proc activechan(x:schannel[SDL_ActiveEvent])
{
  while true do
    handle_active$ read x;
  done;
}

proc resizechan(x:schannel[SDL_ResizeEvent])
{
  while true do
    handle_resize$ read x;
  done;
}

proc drawchan(x:schannel[int], drawing:1->0)
{
  while true do
    var k = read x;
    draw drawing;
  done;
}

proc execute(x:schannel[int], something:1->0)
{
  while true do
    var k = read x;
    something();
  done;
}

val clock = Faio::mk_alarm_clock();
proc poll_event(e:&SDL_Event)
{
tryagain:>
  //print "Polling event"; endl;
  var result = SDL_PollEvent(e);
  if result > 0 do
    //print "Got event"; endl;
    return;
  done;
  Faio::sleep$ clock, 0.1;
  goto tryagain;
}

proc dispatch_event(
  keyboard:schannel[SDL_keysym],
  active:schannel[SDL_ActiveEvent],
  resize:schannel[SDL_ResizeEvent]
)
{
  while true do
    //var &e : SDL_Event <- get_sdl_event event_lock;
    var e : SDL_Event;
    poll_event(&e);
    match get_type e with
    | ?et when et == SDL_ACTIVEEVENT =>
      { write (active, e.active); }

    | ?et when et == SDL_VIDEORESIZE =>
      { write (resize, e.resize); }

    | ?et when et == SDL_KEYDOWN =>
      { write (keyboard, e.key.keysym); }

    | ?et when et == SDL_QUIT =>
      { Quit 0; }

    | _ => {}
    endmatch;
  done;
}

/* write ticks at the desired framerate */
proc framerate (x:schannel[int], framerate:double)
{
  while true do
    Faio::sleep$ clock, framerate;
    write (x,1);
  done;
}

/* LINEAR CONTROL MODEL: CANNOT DEADLOCK
  ~~> async/sync connection
  --> sync/sync connection

  SDL_event ~~> dispatcher
                --> resize handler
                --> active handler
                --> key handler
  timer ~~> framerate --> draw
*/

/* make our communication channels */
var keyboard = mk_schannel[SDL_keysym] ();
var active = mk_schannel[SDL_ActiveEvent] ();
var resize = mk_schannel[SDL_ResizeEvent] ();
var clicks = mk_schannel[int] ();
var rotation = mk_schannel[int] ();

/* start up the fthreads and plug them together */
spawn_fthread { dispatch_event (keyboard, active, resize); };
spawn_fthread { resizechan resize; };
spawn_fthread { activechan active; };
spawn_fthread { keychan keyboard; };

spawn_fthread { drawchan (clicks, Drawing); };
spawn_fthread { framerate (clicks, 0.05); };
spawn_fthread { execute (rotation, rotate); };
spawn_fthread { framerate (rotation, 0.01); };


// main thread hangs

/* Here goes our drawing code */
proc Drawing()
{
  /* Clear The Screen And The Depth Buffer */
  glClear( GL_COLOR_BUFFER_BIT \| GL_DEPTH_BUFFER_BIT );

    /* Select Our Texture */
  glBindTexture( GL_TEXTURE_2D, texture.[0] );
  glLoadIdentity( );

    /* Loop Through All The Stars */
  var i = 0;
  for i in 0 upto NUM - 1 do
    /* Reset The View Before We Draw Each Star */
    glLoadIdentity( );
    /* Zoom Into The Screen (Using The Value In 'zoom') */
    glTranslatef( 0.0f, 0.0f, zoom );

    /* Tilt The View (Using The Value In 'tilt') */
    glRotatef( tilt, 1.0f, 0.0f, 0.0f );
    /* Rotate To The Current Stars Angle */
    glRotatef( stars.[i].angle, 0.0f, 1.0f, 0.0f );

    /* Move Forward On The X Plane */
    glTranslatef( stars.[i].dist, 0.0f, 0.0f );

    /* Cancel The Current Stars Angle */
    glRotatef( -stars.[i].angle, 0.0f, 1.0f, 0.0f );
    /* Cancel The Screen Tilt */
    glRotatef( -tilt, 1.0f, 0.0f, 0.0f );

    /* Twinkling Stars Enabled */
    if twinkle do
      /* Assign A Color Using Bytes */
      glColor4ub( utiny_of stars.[NUM - i  - 1].r,
      utiny_of stars.[NUM - i  - 1].g,
      utiny_of stars.[NUM - i  - 1].b, 255ut );
      /* Begin Drawing The Textured Quad */
      glBegin( GL_QUADS );
        glTexCoord2f( 0.0f, 0.0f );
        glVertex3f( -1.0f, -1.0f, 0.0f );
        glTexCoord2f( 1.0f, 0.0f);
        glVertex3f( 1.0f, -1.0f, 0.0f );
        glTexCoord2f( 1.0f, 1.0f );
        glVertex3f( 1.0f, 1.0f, 0.0f );
        glTexCoord2f( 0.0f, 1.0f );
        glVertex3f( -1.0f, 1.0f, 0.0f );
      glEnd( );
     done;

    /* Rotate The Star On The Z Axis */
    glRotatef( spin, 0.0f, 0.0f, 1.0f );

    /* Assign A Color Using Bytes */
    glColor4ub( utiny_of stars.[i].r, utiny_of stars.[i].g, utiny_of stars.[i].b, 255ut );

    /* Begin Drawing The Textured Quad */
    glBegin( GL_QUADS );
      glTexCoord2f( 0.0f, 0.0f ); glVertex3f( -1.0f, -1.0f, 0.0f );
      glTexCoord2f( 1.0f, 0.0f ); glVertex3f(  1.0f, -1.0f, 0.0f );
      glTexCoord2f( 1.0f, 1.0f ); glVertex3f(  1.0f,  1.0f, 0.0f );
      glTexCoord2f( 0.0f, 1.0f ); glVertex3f( -1.0f,  1.0f, 0.0f );
    glEnd( );

  done;

  /* Draw it to the screen */
  SDL_GL_SwapBuffers( );
}

// REQUIRES OpenGL and GLU
/*
 * This code was created by Jeff Molofee '99
 * (ported to Linux/SDL by Ti Leggett '01)
 *
 * If you've found this code useful, please let me know.
 *
 * Visit Jeff at http://nehe.gamedev.net/
 *
 * or for port-specific comments, questions, bugreports etc.
 * email to leggett@eecs.tulane.edu
 */

include "SDL/SDL";
open C_hack;
open Carray;
open MixedInt;

/* screen width, height, and bit depth */
val SCREEN_WIDTH  = 640;
val SCREEN_HEIGHT = 480;
val SCREEN_BPP = 16;

/* function to reset our viewport after a window resize */
proc resizeWindow( wwidth : int, hheight :int)
{
  var height = hheight;
  var width = wwidth;

  /* Protect against a divide by zero */
  if height == 0 do height = 1; done;
  var ratio = double_of width / double_of height;

  block_sdl_events event_lock;
  /* Setup our viewport. */
  glViewport( 0, 0, width, height );

  /* change to the projection matrix and set our viewing volume. */
  glMatrixMode( GL_PROJECTION );
  glLoadIdentity( );

  /* Set our perspective */
  gluPerspective( 45.0, ratio, 0.1, 100.0 );

  /* Make sure we're chaning the model view and not the projection */
  glMatrixMode( GL_MODELVIEW );

  /* Reset The View */
  glLoadIdentity( );
  unblock_sdl_events event_lock;
}

/* general OpenGL initialization function */
proc initGL()
{
  /* Enable smooth shading */
  glShadeModel( GL_SMOOTH );

  /* Set the background black */
  glClearColor( 0.0f, 0.0f, 0.0f, 0.0f );

  /* Depth buffer setup */
  glClearDepth( 1.0 );

  /* Enables Depth Testing */
  glEnable( GL_DEPTH_TEST );

  /* The Type Of Depth Test To Do */
  glDepthFunc( GL_LEQUAL );

  /* Really Nice Perspective Calculations */
  glHint( GL_PERSPECTIVE_CORRECTION_HINT, GL_NICEST );
}

/* These are to calculate our fps */
var T0     = 0u;
var Frames = 0;


/* Here goes our drawing code */
proc drawGLScene(drawing:1->0)
{
  block_sdl_events event_lock;
  drawing();
  unblock_sdl_events event_lock;

  /* Gather our frames per second */
  Frames++;
  {
    var t = SDL_GetTicks();
    if t - T0 >= 5000 do
      val seconds = double_of (t - T0) / 1000.0;
      val fps = double_of Frames / seconds;
      print Frames; print " frames in "; print seconds;
      print " seconds = "; print fps; print " FPS"; endl;
      T0 = t;
      Frames = 0;
    done;
  };
}

/* whether or not the window is active */
var isActive = true;

if SDL_Init(SDL_INIT_AUDIO \| SDL_INIT_VIDEO) < 0 do
  print "Unable to init SDL"; endl;
  System::exit(1);
done;

var event_lock = SDL_CreateMutex();


proc Quit(n:int)
{
  SDL_Quit;
  System::exit 0;
}

/* Fetch the video info */
var videoInfo = SDL_GetVideoInfo();

if isNULL videoInfo do
  print "Video query failed"; endl;
  Quit 1;
done;

/* the flags to pass to SDL_SetVideoMode */
var
  videoFlags  = SDL_OPENGL;          /* Enable OpenGL in SDL */
  videoFlags |= cast[uint] SDL_GL_DOUBLEBUFFER; /* Enable double buffering */
  videoFlags |= SDL_HWPALETTE;       /* Store the palette in hardware */
  videoFlags |= SDL_RESIZABLE;       /* Enable window resizing */

/* This checks to see if surfaces can be stored in memory */
if (*videoInfo).hw_available != 0 do
    videoFlags |= SDL_HWSURFACE;
else
    videoFlags |= SDL_SWSURFACE;
done;

/* This checks if hardware blits can be done */
if (*videoInfo).blit_hw != 0 do
    videoFlags |= SDL_HWACCEL;
done;

/* Sets up OpenGL double buffering */
ignore$ SDL_GL_SetAttribute( cast[SDL_GLattr] SDL_GL_DOUBLEBUFFER, 1 );

/* get a SDL surface */
var surface = SDL_SetVideoMode
  (SCREEN_WIDTH, SCREEN_HEIGHT, SCREEN_BPP, videoFlags )
;

/* Verify there is a surface */
if isNULL surface do
  print "Video mode set failed"; endl;
  Quit 1;
done;

/* initialize OpenGL */
initGL();
var gl_vendor = str$ cast[&char]$ glGetString(GL_VENDOR);
var gl_renderer = str$ cast[&char]$ glGetString(GL_RENDERER);
var gl_version = str$ cast[&char]$ glGetString(GL_VERSION);
var gl_extensions = str$ cast[&char]$ glGetString(GL_EXTENSIONS);

print$ "GL vendor = " + gl_vendor; endl;
print$ "GL renderer = " + gl_renderer; endl;
print$ "GL version = " + gl_version; endl;
print$ "GL extensions = " + gl_extensions; endl;

/* resize the initial window */
resizeWindow( SCREEN_WIDTH, SCREEN_HEIGHT );

// SDL Event dispatcher
proc handle_active (e:SDL_ActiveEvent)
{
   isActive = e.gain != 0;
}

proc handle_resize(e:SDL_ResizeEvent)
{
  block_sdl_events event_lock;
  surface = SDL_SetVideoMode(
    e.w,
    e.h,
    16, videoFlags
  );
  if isNULL surface do
    print "Could not get a surface after resize"; endl;
  done;
  resizeWindow( e.w, e.h );
  unblock_sdl_events event_lock;
}

/* function to handle key press events */
proc handle_key( keysym : SDL_keysym)
{
  match keysym.sym with
  | ?k when k == SDLK_ESCAPE => { Quit 0; }
  | ?k when k ==  SDLK_F1 =>
        {
        block_sdl_events event_lock;
        ignore$ SDL_WM_ToggleFullScreen( surface );
        unblock_sdl_events event_lock;
      }
  | _ => {}
  endmatch;
}

/* draw the scene */
proc draw(drawing: 1->0) {
  if isActive call drawGLScene( drawing );
}

proc keychan(x:schannel[SDL_keysym])
{
  while true do
    handle_key$ read x;
  done;
}

proc activechan(x:schannel[SDL_ActiveEvent])
{
  while true do
    handle_active$ read x;
  done;
}

proc resizechan(x:schannel[SDL_ResizeEvent])
{
  while true do
    handle_resize$ read x;
  done;
}

proc drawchan(x:schannel[int], drawing:1->0)
{
  while true do
    var k = read x;
    draw drawing;
  done;
}

val clock = Faio::mk_alarm_clock();
proc poll_event(e:&SDL_Event)
{
tryagain:>
  //print "Polling event"; endl;
  var result = SDL_PollEvent(e);
  if result > 0 do
    //print "Got event"; endl;
    return;
  done;
  Faio::sleep$ clock, 0.1;
  goto tryagain;
}

proc dispatch_event(
  keyboard:schannel[SDL_keysym],
  active:schannel[SDL_ActiveEvent],
  resize:schannel[SDL_ResizeEvent]
)
{
  while true do
    // var &e : SDL_Event <- get_sdl_event event_lock;
    var e : SDL_Event;
    poll_event(&e);

    match get_type e with
    | ?et when et == SDL_ACTIVEEVENT =>
      { write (active, e.active); }

    | ?et when et == SDL_VIDEORESIZE =>
      { write (resize, e.resize); }

    | ?et when et == SDL_KEYDOWN =>
      { write (keyboard, e.key.keysym); }

    | ?et when et == SDL_QUIT =>
      { Quit 0; }

    | _ => {}
    endmatch;
  done;
}

/* write ticks at the desired framerate */
proc framerate (x:schannel[int], framerate:double)
{
  while true do
    Faio::sleep$ clock, framerate;
    write (x,1);
  done;
}

/* LINEAR CONTROL MODEL: CANNOT DEADLOCK
  ~~> async/sync connection
  --> sync/sync connection

  SDL_event ~~> dispatcher
                --> resize handler
                --> active handler
                --> key handler
  timer ~~> framerate --> draw
*/

/* make our communication channels */
var keyboard = mk_schannel[SDL_keysym] ();
var active = mk_schannel[SDL_ActiveEvent] ();
var resize = mk_schannel[SDL_ResizeEvent] ();
var clicks = mk_schannel[int] ();

/* start up the fthreads and plug them together */
spawn_fthread { dispatch_event (keyboard, active, resize); };
spawn_fthread { resizechan resize; };
spawn_fthread { activechan active; };
spawn_fthread { keychan keyboard; };

spawn_fthread { drawchan (clicks, Drawing); };
spawn_fthread { framerate (clicks, 0.1); };

// main thread hangs

// THE DRAWING
proc Drawing()
{
  /* Clear The Screen And The Depth Buffer */
  glClear( GL_COLOR_BUFFER_BIT \| GL_DEPTH_BUFFER_BIT );

  /* Move Left 1.5 Units And Into The Screen 6.0 */
  glLoadIdentity();
  glTranslatef( -1.5f, 0.0f, -6.0f );

  glBegin( GL_TRIANGLES );            /* Drawing Using Triangles */
    glVertex3f(  0.0f,  1.0f, 0.0f ); /* Top */
    glVertex3f( -1.0f, -1.0f, 0.0f ); /* Bottom Left */
    glVertex3f(  1.0f, -1.0f, 0.0f ); /* Bottom Right */
  glEnd( );                           /* Finished Drawing The Triangle */

  /* Move Right 3 Units */
  glTranslatef( 3.0f, 0.0f, 0.0f );

  glBegin( GL_QUADS );                /* Draw A Quad */
    glVertex3f( -1.0f,  1.0f, 0.0f ); /* Top Left */
    glVertex3f(  1.0f,  1.0f, 0.0f ); /* Top Right */
    glVertex3f(  1.0f, -1.0f, 0.0f ); /* Bottom Right */
    glVertex3f( -1.0f, -1.0f, 0.0f ); /* Bottom Left */
  glEnd( );                           /* Done Drawing The Quad */

  /* Draw it to the screen */
  SDL_GL_SwapBuffers( );
}

var prefix = System::argv_dflt 1 "tut";
var title = System::argv_dflt 2 "Tutorial";
var master = System::argv_dflt 3 "";
if prefix == "--help" do
  println "Usage flx_mktutindex tutname title homepage";
  println "  Makes src/web/tut/tutname_index.fdoc for files in src/web/tut/tutname_\\d*\\.fdoc";
  System::exit 0;
done

re := RE2(prefix+"_\\d*\\.fdoc");
docs := FileSystem::regfilesin("src/web/tut", re);
iter println of (string) docs;
f := fopen_output("src/web/tut/"+prefix+"_index.fdoc");
if master != "" do
  writeln$ f,
   "<p><a href='"+master+"'>Up</a></p>"
  ; 
done

writeln$ f,"@h1 "+title+" Index";
writeln$ f,"<ul>";
iter (proc (x:string) { writeln$ f, mkentry x; }) docs;
writeln$ f,"</ul>";
fclose f;

fun mkentry(x:string):string = 
{
  f := fopen_input("src/web/tut/"+x);
  hline := f.readln;
  fclose f;
  title := hline.strip.[4 to];
  html := '<li><a href="' + Filename::basename x + '">' + title + '</a></li>';
  return html;
}


var ishtml = System::argv 1 == "--html";
var dir =  Filename::join #(Config::config).FLX_INSTALL_DIR "lib";
var fregex = ".*\\.flx";
var lregex = "^ *(virtual|noinline)* *(proc|fun|class|ctor|gen) *(([A-Z]|[a-z])([A-Z]|[a-z]|[0-9]|-|_)*).*";
var lgrep = RE2 lregex;
var n = NumberOfCapturingGroups(lgrep)+1;
var v = varray[StringPiece] (n.size,StringPiece "");

var grexp = RE2 lregex;
var extract = RE2 " *([^={]*) *(=|{|;).*";
var n2 = NumberOfCapturingGroups(extract)+1;
var v2 = varray[StringPiece] (n2.size,StringPiece "");
var v2a = varray[StringPiece] (n2.size,StringPiece "");

if ishtml do
  println$ "<html><body>";
  println$ "<h1>Felix Library Contents</h1>";
 println$ "<pre>";
done

for file in FileSystem::regfilesin (dir, fregex) do
  var href = "/$"+dir+"/"+file; // URL always uses Unix filenames
  if ishtml do
    println$ '<hr/><a href="'+href+'">'+file+'</a>';
  else
    println$ file;
  done
  var lines = load (Filename::join dir file);
  var count = 0;
  for line in split (lines,char "\n") do
    ++count;
    var m = Match (grexp, StringPiece line, 0, ANCHOR_BOTH, v.stl_begin,n); 
    if m do
      var sym = v.3.string;
      var dfn = "";
      var m2 = Match (extract, StringPiece line, 0, ANCHOR_BOTH, v2.stl_begin, n2);
      if m2 do
        m2 = Match (extract, StringPiece line, 0, ANCHOR_BOTH, v2a.stl_begin, n2);
        if m2 do
          dfn = v2a . 1 . string . strip;
        else
          dfn = v2 . 1 . string . strip;
        done
      else
        dfn = line . strip;
      done
      if ishtml do
        if prefix (dfn, "class") do
          println$  f"%04d" count + ":  " + '<a href="'+href+'#'+f"%04d" count+'">'+dfn +'</a>';
        else
          println$ f"%04d" count + ":    " + '<a href="'+href+'#'+f"%04d" count+'">'+ dfn +'</a>';
        done
      else
        if prefix (dfn, "class") do
          println$ f"%04d" count + ":  " + dfn;
        else
          println$ f"%04d" count + ":    " + dfn;
        done
      done
    done
  done
done

if ishtml do
  println$ "</pre></body></html>";
done


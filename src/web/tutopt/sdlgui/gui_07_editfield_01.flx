
include "gui/__init__";
open FlxGui;

println$ "Basic Edit Field Test";

var clock = Faio::mk_alarm_clock();

// Initialise the SDL system (SDL + TTF + IMAGE)
FlxGui::init();

var font_name = dflt_mono_font();
var font : font_t = get_font(font_name, 12);
var lineskip = get_lineskip font;

proc mk_field 
(
  window:window_t, x:int, y:int, 
  font:font_t, colour: colour_t, bgcolour:colour_t, 
  dflt:string,
  ed: &line_buffer_interface,
  o:&oschannel[event_t]
) 
{
  println$ "mk_field ("+x.str +"," + y.str+")="+dflt;
  var editor = line_buffer (dflt.len.int, dflt);
  var dc = line_buffer_display_controller (window,font,colour,bgcolour,x,y,editor);
  dc.display();
  var ich,och = #(mk_ioschannel_pair[event_t]);
  var controller = line_edit editor dc ich;
  spawn_fthread controller; 
  println$ "mkfield: controller spawned";
  ed <- editor;
  o <- och;
}

// make an event handler for our window
proc ehandler 
(
  fields: varray[oschannel[event_t]] 
) 
(input:ischannel[event_t])  () 
{
  println$ "Window event handler running";
  var selected_field = 0;
  var run = true;
  while run do
    var ev: event_t = read input;
    //if ev.type.SDL_EventType != SDL_MOUSEMOTION do
    //  println$ "  WINDOW specific EVENT: " + ev.window.event.SDL_WindowEventID.str;
    //done
    if 
      ev.type.SDL_EventType == SDL_KEYDOWN and 
      ev.key.keysym.sym \in (SDLK_TAB, SDLK_RETURN) 
    do
      var sel :SDL_Event;
      &sel.type <- SDL_WINDOWEVENT.uint32;
      sel.window.event = SDL_WINDOWEVENT_FOCUS_LOST.uint8;
      write$ fields.selected_field,sel;
      selected_field = (selected_field + 1) % fields.len.int;
      sel.window.event = SDL_WINDOWEVENT_FOCUS_GAINED.uint8;
      write$ fields.selected_field,sel;
    done
    write$ fields.selected_field, ev;
    run = ev.window.event.SDL_WindowEventID != SDL_WINDOWEVENT_CLOSE ;
  done
  println$ "Terminating window event processing loop";
}

 
begin
  //create a window manager
  var wm = window_manager();

  // create a window
  var w1 = window_t("Felix:gui_07_editfield_01",100,100,400,400);
  w1.clear lightgrey;
  w1.write (100,10,font,black,"Basic Field Edit Test");
  w1.update;

  var ch1: oschannel[event_t]; var ed1: line_buffer_interface;
  mk_field (w1, 50, 60, font, black, white, "EDITFIELD ONE", &ed1, &ch1);
  
  var ch2: oschannel[event_t]; var ed2: line_buffer_interface; 
  mk_field (w1, 50, 120, font, black, white, "EDITFIELD TWO", &ed2, &ch2);

  var ch3: oschannel[event_t]; var ed3: line_buffer_interface; 
  mk_field (w1, 50, 180, font, black, white, "EDITFIELD THREE", &ed3, &ch3);

  var fields = varray[oschannel[event_t]] (ch1, ch2, ch3);

  // create a window controller for our window
  var wc1 = window_controller (w1, ehandler fields);

  // attach controller to window manager
  var wno = wm.add_window wc1;

  // USE variable wno or Felix will elide the call!
  println$ "Window number " + wno.str;

  // get the channel to read events from
  var input = wm.get_event_source();
    
  // read an event
  var e= read input;

  while e.type.SDL_EventType != SDL_QUIT do

    // dispatch the event to the window it is associated with
    wm.dispatch_window_event e;

    // get another event
    e= read input;
  done

  println$ "ED1='"+ed1.get()+ "'";
  println$ "ED2='"+ed2.get()+ "'";
  println$ "ED3='"+ed3.get()+ "'";


  // we must have got a quit ..
  println$ "QUIT EVENT";
  wm.delete_all();
  println$ "Windows deleted, quitting";
end



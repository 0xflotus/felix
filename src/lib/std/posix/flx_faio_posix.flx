// contains posix async socket io & copipes, all wrapped up streams

include "pthread";
include "std/io/flx_faio"; //mutual recursion? Really!
include "std/io/flx_demux";

module Faio_posix  {
header faio_posixio_hpp = '#include "faio_posixio.hpp"';
requires package "demux";
requires package "faio";
open C_hack;        // cast, address
open Faio;
open Pthread;
open Demux;

header unistd_h = '#include <unistd.h>';            // close
header fcntl_h = '#include <fcntl.h>';              // fcntl for O_NONBLOCK
header sys_stat_h = '#include <fcntl.h>';              // for S_* permissions
header sys_socket_h = '#include <sys/socket.h>';    // shutdown
header sockety_h = '#include "demux_sockety.hpp"';  // my socket utils
header = '#include "faio_posixio.hpp"';


type fd_t = "int";
fun invalid: fd_t -> bool="$1==-1";

instance Str[fd_t] {
  fun str: fd_t -> string = "flx::rtl::strutil::str<int>($1)" requires flx_strutil;
}

instance Str[socket_t] {
  fun str: socket_t -> string = "flx::rtl::strutil::str<int>($1)" requires flx_strutil;
}

proc close: socket_t = 'close($1);' requires unistd_h;
proc close: fd_t = 'close($1);' requires unistd_h;

proc shutdown: socket_t*int = 'shutdown($a);' requires sys_socket_h;

fun bad_socket : socket_t -> bool = "$1 == -1";

type posix_permissions = "mode_t" requires sys_stat_h;
const S_IRUSR : posix_permissions;
const S_IWUSR : posix_permissions;
const S_IXUSR : posix_permissions;
const S_IRGRP : posix_permissions;
const S_IWGRP : posix_permissions;
const S_IXGRP : posix_permissions;
const S_IROTH : posix_permissions;
const S_IWOTH : posix_permissions;
const S_IXOTH : posix_permissions;

// non blocking
/*
gen aio_ropen: string -> fd_t = 'open($1.c_str(), O_RDONLY | O_NONBLOCK)'
    requires fcntl_h, sys_stat_h;
gen aio_wopen: string -> fd_t = ' open($1.c_str(), O_WRONLY | O_NONBLOCK | O_CREAT | O_TRUNC, S_IRUSR|S_IWUSR)'
    requires fcntl_h, sys_stat_h;
gen aio_rwopen: string -> fd_t = ' open($1.c_str(), O_RDWR | O_NONBLOCK | O_CREAT | O_TRUNC, S_IRUSR|S_IWUSR)'
    requires fcntl_h, sys_stat_h;
gen aio_creat: string * posix_permissions-> fd_t = ' open($1.c_str(), O_RDWR | O_NONBLOCK | O_CREAT | O_TRUNC, $2)'
    requires fcntl_h, sys_stat_h;
*/


// blocking
gen ropen: string -> fd_t = 'open($1.data(), O_RDONLY,0)' requires fcntl_h, sys_stat_h;
gen wopen: string -> fd_t = 'open($1.data(), O_WRONLY | O_CREAT | O_TRUNC, S_IRUSR | S_IWUSR)' requires fcntl_h, sys_stat_h;
gen rwopen: string -> fd_t = 'open($1.data(), O_RDWR,0)' requires fcntl_h, sys_stat_h;
gen creat: string * posix_permissions -> fd_t = 'open($1.data(), O_WRONLY | O_CREAT | O_TRUNC, $2)' requires fcntl_h, sys_stat_h;

fun access: string -> posix_permissions = "get_perm($1.data())"
  requires body """
  mode_t get_perm(char const *f)
  {
    struct stat b;
    stat(f,&b);
    return b.st_mode;
  }
  """
;

fun access: fd_t -> posix_permissions = "get_perm($1)"
  requires body """
  mode_t get_perm(int f)
  {
    struct stat b;
    fstat(f,&b);
    return b.st_mode;
  }
  """
;

type socket_t = "int";
// socketio_request should be renamed to be async_fd_request
type socketio_request = "flx::faio::socketio_request";

gen mk_socketio_request: demuxer * socket_t*address*int*bool -> socketio_request
    = 'flx::faio::socketio_request($1, $2, (char*)$3, $4, $5)';

fun get_pb: socketio_request -> sel_param_ptr = '&$1.sv.pb';

// read & write differ only by a flag
proc async_rw(fd: socket_t, len: &int, buf: address, eof: &bool, read_flag: bool)
{
    //println$ "faio/flx_faoi_posix.flx: async_rw (s,"+str (*len)+",buf,"+str(*eof)+", "+str read_flag+") calling mk_socketio_req ..";
    var asyncb = mk_socketio_request(sys_demux,fd, buf, *len, read_flag);
    faio_req$ &asyncb;
    //println$ "faio/flx_faoi_posix.flx: async_rw ("+ str fd+", "+str (*len)+",buf,"+str(*eof)+", "+str read_flag+") calculating eof ..";

    calc_eof(asyncb.pb, len, eof);
    //println$ "faio/flx_faoi_posix.flx: async_rw (s,"+str (*len)+",buf,"+str(*eof)+", "+str read_flag+") called mk_socketio_req ..";
}

proc async_read(fd: socket_t, len: &int, buf: address,
    eof: &bool)
{
    async_rw(fd, len, buf, eof, true);      // read
}

proc async_write(fd: socket_t, len: &int, buf: address, eof: &bool)
{
    //println$ "faio/flx_faoi_posix.flx: async_write(s,"+str (*len)+",buf,"+str(*eof)+" calling async_rw ..";
    async_rw(fd, len, buf, eof, false);     // write
    //println$ "faio/flx_faoi_posix.flx: async_write(s,"+str (*len)+",buf,"+str(*eof)+" call async_rw ..";
}

type flxfileio_request = "flx::faio::flxfileio_request";

// connect!
type async_connect = 'flx::faio::connect_request';

fun mk_async_connect: demuxer * charp*int-> async_connect = 'flx::faio::connect_request($a)';
fun get_socket: async_connect -> socket_t = '$1.s';
fun get_err: async_connect -> int = '$1.socket_err';

// could do multi connects for capable drivers
proc connect(s: &socket_t, addr: charp, port: int)
{
    var ac = mk_async_connect(sys_demux,addr, port);
    faio_req$ &ac;
    *s = ac.socket;
}

type accept_request = "flx::faio::accept_request";

fun mk_accept: demuxer * socket_t -> accept_request = 'flx::faio::accept_request($1,$2)';
fun get_socket: accept_request -> socket_t = '$1.accepted';

// arg1 = returned socket, arg2 is port, pass 0 to have one assigned
proc mk_listener: &socket_t* &int *int
    = '*$1 = flx::demux::create_async_listener($2, $3);' requires sockety_h;

proc accept(s: &socket_t, listener: socket_t)
{
    var acc = mk_accept$ sys_demux,listener;
    faio_req$ &acc;
    *s = acc.socket;
}

// ASYNC FILE IO

// offset ? let it be for a moment
fun mk_faio: job_queue * fd_t*address*int*int*bool -> flxfileio_request
    = 'flx::faio::flxfileio_request($1,$2, (char*)$3, $4, $5, $6)';
fun get_pb: flxfileio_request -> sel_param_ptr = '&$1.pb';

proc faio_rw(q:job_queue, fd: fd_t, len: &int, buf: address, eof: &bool, read_flag: bool)
{
    // constant offset for now, rushing to get this in flx_stream
    var faio = mk_faio(q, fd, buf, *len, 0, read_flag);
    faio_req$ &faio;
    //print$ f"faio_rw: request %d, actual %d\n" (*len,faio.pb.bytes_done);
    calc_eof(faio.pb, len, eof);
}

// HACKERY -- system job queue
val qbound = 20;
val nthreads = 4;
val sys_job_queue = Pthread::mk_job_queue(qbound,nthreads);

proc faio_read(fd: fd_t, len: &int, buf: address,
    eof: &bool)
{
    faio_rw(sys_job_queue, fd, len, buf, eof, true);       // read
}

proc faio_write(fd: fd_t, len: &int, buf: address, eof: &bool)
{
    faio_rw(sys_job_queue, fd, len, buf, eof, false);      // write
}

} // module faio_posix

//$ These classes provide simple I/O for text, primarily intended for
//$ naive use, debugging etc. This is because there is no error
//$ handling. This simplifies usage at the expense of correctness,
//$ and so these routines should not be used in production code.

//$ Abstract input file.
class Input_text_file[input_text_file]
{
  //$ Open file for reading.
  virtual gen fopen_input: string -> input_text_file; 

  //$ Check if the file was opened correctly.
  virtual gen valid : input_text_file -> bool;
 
  //$ Close file.
  virtual proc fclose: input_text_file;

  //$ Load the rest of an open file.
  virtual gen load: input_text_file -> string;

  //$ Read one line with the trailing end-line mark included.
  //$ Empty string indicates end of file.
  virtual gen readln: input_text_file -> string;

  //$ Check for end of file.
  virtual gen feof : input_text_file -> bool;
}

//$ Abstract output file.
class Output_text_file[output_text_file]
{
  //$ Open file for writing.
  virtual gen fopen_output: string -> output_text_file;

  //$ Check if the file was opened correctly.
  virtual gen valid : output_text_file -> bool;
 
  //$ Close file.
  virtual proc fclose: output_text_file;

  //$ Write one line adding the trailing end line mark.
  virtual proc writeln : output_text_file * string;

  //$ Write a string.
  virtual proc write : output_text_file * string;

  //$ Write a byte.
  virtual proc write : output_text_file * utiny;

  //$ Write a char.
  virtual proc write : output_text_file * char;

  //$ Flush the buffers.
  virtual proc fflush: output_text_file;

  //$ Write a space.
  proc space (s:output_text_file) { write (s, " "); };

  //$ Write end of line mark.
  proc endl (s:output_text_file) { write (s, "\n"); };

  //$ Write data with conversion using Str::str.
  proc fprint[T with Str[T]] (s:output_text_file, x:T) { write (s, str x); };

  //$ Write data with conversion using Str::str and end line mark.
  proc fprintln[T with Str[T]] (s:output_text_file, x:T) { write (s, str x+"\n"); };
}

//$ C standard IO with FILE*.
open class Cstdio {

  //$ C file type.
  type FILE = "::std::FILE*" requires Cxx_headers::cstdio;

  pod type ifile = "::std::FILE*" requires Cxx_headers::cstdio;
  pod type ofile = "::std::FILE*" requires Cxx_headers::cstdio;

  //$ Load file from filename.
  //$ Note: loaded in binary mode not text mode!
  fun load: string -> string = "::flx::rtl::ioutil::load_file($1)"
    requires package "flx_ioutil";


  //$ Standard input, can be redirected by flx_run.
  const stdin: ifile = "PTF flx_stdin" requires property "needs_ptf";

  //$ Standard output, can be redirected by flx_run.
  const stdout: ofile = "PTF flx_stdout" requires property "needs_ptf";

  //$ Standard error, can be redirected by flx_run.
  const stderr: ofile = "PTF flx_stderr" requires property "needs_ptf";

  //$ Standard input, redirected by shell.
  const cstdin: ifile = "::std::stdin";

  //$ Standard output, redirected by shell.
  const cstdout: ofile = "::std::stdout";

  //$ Standard error, redirected by shell.
  const cstderr: ofile = "::std::stderr";

  //$ C standard IO as instance of Input_text_file.
  instance Input_text_file[ifile] {
    requires package "flx_ioutil";
    gen fopen_input: string -> ifile = '::std::fopen($1.c_str(),"rt")';
    gen valid : ifile -> bool = "$1!=(FILE*)0";
    proc fclose: ifile = '(void)::std::fclose($1);';
    gen load: ifile -> string = "::flx::rtl::ioutil::load_file($1)";
    gen readln: ifile -> string ="::flx::rtl::ioutil::readln($1)";
    gen feof : ifile -> bool = "::std::feof($1)";
  }

  //$ C standard IO as instance of Output_text_file.
  instance Output_text_file[ofile] {
    requires package "flx_ioutil";
    gen fopen_output: string -> ofile = '::std::fopen($1.c_str(),"wt")';
    gen valid : ofile -> bool = "$1!=(FILE*)0";
    proc fclose: ofile = '(void)::std::fclose($1);';
    proc writeln : ofile * string ="::flx::rtl::ioutil::writeln($1,$2);";
    proc write : ofile * string ="::flx::rtl::ioutil::write($1,$2);";
    proc write : ofile * utiny ="::std::fwrite($2,1,1,$1);";
    proc write : ofile * char ="::std::fwrite($2,1,1,$1);";
    proc fflush: ofile = "::std::fflush($1);";
  }
}

open Input_text_file[Cstdio::ifile];
open Output_text_file[Cstdio::ofile];

// C++ I/O streams.
open class Iostream {

  //$ istream type.
  pod type istream = "::std::istream*" requires Cxx_headers::fstream;

  //$ ostream type.
  pod type ostream = "::std::ostream*" requires Cxx_headers::fstream;

  //$ Standard input, redirected by shell.
  const cin : istream = "&::std::cin" requires Cxx_headers::iostream;

  //$ Standard output, redirected by shell.
  const cout: ostream = "&::std::cout" requires Cxx_headers::iostream;

  //$ Standard error, redirected by shell.
  const cerr: ostream = "&::std::cerr" requires Cxx_headers::iostream;

  //$ Standard log, redirected by shell.
  const clog : ostream = "&::std::clog" requires Cxx_headers::iostream;

  //$ C++ istream as instance of Input_text_file.
  instance Input_text_file[istream] {
    requires package "flx_ioutil";
    gen fopen_input: string -> istream = '::std::fopen($1.c_str(),"rt")';
    gen valid : istream -> bool = "!$1->fail()";
    proc fclose: istream = '$1->close();';
    gen load: istream -> string = "::flx::rtl::ioutil::load_file($1)";
    gen readln: istream -> string ="::flx::rtl::ioutil::readln($1)";
    gen feof : istream -> bool = "$1->eof() || $1->fail()";
  }

  //$ C++ ostream as instance of Output_text_file.
  instance Output_text_file[ostream] {
    requires package "flx_ioutil";
    gen fopen_output: string -> ostream = '::std::fopen($1.c_str(),"wt")';
    gen valid : ostream -> bool = "!$1->fail()";
    proc fclose: ostream = '$1->setstate(::std::ios::eofbit);';
    proc writeln : ostream * string ="::flx::rtl::ioutil::writeln($1,$2);";
    proc write : ostream * string ="::flx::rtl::ioutil::write($1,$2);";
    proc write : ostream * utiny ="$1->put($2);";
    proc write : ostream * char ="$1->put($2);";
    proc fflush: ostream = "$1->flush();";
  }
}


open Input_text_file[Iostream::istream];
open Output_text_file[Iostream::ostream];

//$ Write string to cout.
proc print  [T with Str[T]] (x:T) { fprint (cout, x); };

//$ Write string to cout with end of line.
proc println[T with Str[T]] (x:T) { fprintln (cout, x); };

//$ Write end of line on cout.
proc endl() { endl cout; }

//$ Write space on cout.
proc space() { space cout; }

//$ flush buffers of cout.
proc fflush() { fflush cout; }

//$ Write string to cerr.
proc eprint  [T with Str[T]] (x:T) { fprint (cerr, x); };

//$ Write string to cerr with end of line.
proc eprintln[T with Str[T]] (x:T) { fprintln (cerr, x);};

//$ Write end of line on cerr.
proc eendl() { endl cerr; }

//$ Write space on cerr.
proc espace() { space cerr; }


include "std/version";

class Config {

  fun config () = 
  {
    fun / (x:string, y:string) => Filename::join (x,y);

    var re = RE2 ("([-a-zA-Z]+) *: *(.*)");

    // global defaults
    var PREFIX = Filename::join ("/usr","local","lib");
    var HOME = Env::getenv("HOME");

    var INSTALL_ROOT_TOPDIR= Filename::join (PREFIX,"felix");
    var INSTALL_ROOT = Filename::join (INSTALL_ROOT_TOPDIR, "felix-"+Version::felix_version);
    var FLX_INSTALL_DIR = INSTALL_ROOT;
    var FLX_SHARE_DIR = FLX_INSTALL_DIR/"share";
    var FLX_TARGET_SUBDIR = "host";
    var FLX_TARGET_DIR = FLX_INSTALL_DIR / FLX_TARGET_SUBDIR;

    // try HOME/.felix/config/felix.fpc

    var CACHE_DIR = HOME/"felix"/"cache"/"binary";

    var felix_dir = Filename::join (HOME, ".felix");
    var config_dir = Filename::join (felix_dir, "config");
    var control_file = Filename::join (config_dir, "felix.fpc");
    var text = load control_file;
    if text == "" do
      println$ "config::WARNING: No $HOME/.felix/config/felix.fpc file!";
    done
    var lines = split (text, char "\n");
    for line in lines do
      var found = Match (re, line);
      match found with
      | Some ?v when v.len.int == 3 => 
        var p = v.1;
        var a = strip v.2;
        match p with
        | "FLX_INSTALL_DIR" => FLX_INSTALL_DIR = a; 

        | "FLX_TARGET_SUBDIR" => FLX_TARGET_SUBDIR = a; 
          FLX_TARGET_DIR = FLX_INSTALL_DIR / FLX_TARGET_SUBDIR;

        | "FLX_SHARE_DIR" => FLX_SHARE_DIR = a; 
        | "FLX_TARGET_DIR" => FLX_TARGET_DIR = a; 
        endmatch;
      | None => ;
      endmatch;
    done

    // try environment
    var env_flx_install_dir = Env::getenv ("FLX_INSTALL_DIR");
    if env_flx_install_dir != "" do
      FLX_INSTALL_DIR = env_flx_install_dir;
      //println$ "Resetting FLX_INSTALL_DIR from env to '" + FLX_INSTALL_DIR+"'";
    done

    // final result can still be overriden by command line switch
    //println$ "FLX_INSTALL_DIR='" + FLX_INSTALL_DIR+"'";
    //println$ "FLX_TARGET_DIR='" + FLX_TARGET_DIR+"'";
    return (FLX_SHARE_DIR=FLX_SHARE_DIR, FLX_TARGET_DIR=FLX_TARGET_DIR);
  }

  typedef config_type = typeof (#config);
}


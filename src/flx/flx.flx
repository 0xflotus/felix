include "plat/config";
include "std/posix/process";

// Changed to just copy values out of the library
// Change to use structs
var INSTALL_ROOT_TOPDIR=Config::INSTALL_ROOT_TOPDIR;
var INSTALL_ROOT=Config::INSTALL_ROOT;
var FLX_INSTALL_DIR=Config::FLX_INSTALL_DIR;
var CYGWIN=Config::CYGWIN;
var WIN32=Config::WIN32;
var MACOSX=Config::MACOSX;
var HAVE_GNU=Config::HAVE_GNU;
var HAVE_MSVC=Config::HAVE_MSVC;
var CCOBJ_DLLIB=Config::CCOBJ_DLLIB;
var CCLINK_DLLIB=Config::CCLINK_DLLIB;
var CCOBJ_STATIC_LIB=Config::CCOBJ_STATIC_LIB;
var CCLINK_STATIC=Config::CCLINK_STATIC;
var VERSION=Config::VERSION;
var EXT_LIB=Config::EXT_LIB;
var EXT_STATIC_OBJ=Config::EXT_STATIC_OBJ;
var EXT_SHARED_OBJ=Config::EXT_SHARED_OBJ;
var EXT_EXE=Config::EXT_EXE;
var EXT_SHLIB=Config::EXT_SHLIB;
var SPEC_OBJ_FILENAME=Config::SPEC_OBJ_FILENAME;
var SPEC_EXE_FILENAME=Config::SPEC_EXE_FILENAME;
var OPTIMISE=Config::OPTIMISE;
var DEBUG_FLAGS=Config::DEBUG_FLAGS;
var DEFAULT_LINK_STATIC=Config::DEFAULT_LINK_STATIC;

dbug := false; // switch off for production
var RECOMPILE=0;
var DEBUG=0;
var DEBUG_COMPILER=0;
var INLINE=100;
var ECHO=0;
var TIME=0;
var NOOPTIMISE=0;
var TIMECMD="time -p";
var STATIC=DEFAULT_LINK_STATIC;
var SHOWCODE=0;
var RUNIT=1;
var CCFLAGS="";
var FELIX=1;
var LINKER_SWITCHES="";
var MACROS="";
var grab=1;
var cpps="";
var cppos="";
var INCLUDE_DIRS="";
var INCLUDE_FILES="";
var NOSTDLIB=0;
var STDOUT="";
var STDIMPORT="--import=nugram.flxh --import=plat/flx.flxh";
var IMPORTS="";
var OUTPUT_DIR="";
var CACHE_DIR="";

var DRIVER_PKG = "";
var DRIVER = "";
var LINK_STRING = "";

var pkgs="";

var CONFIG_DIR = "";
var FLXG = "";
var FLXRUN = "";

var DLINK_STRING:string;
var SLINK_STRING:string;

fun splitext(p:string)=>
  if p.[-4 to] == ".flx" then p.[to -4],"flx"
  elif p.[-4 to] == ".cpp" then p.[to -4],"cpp"
  else p,""
  endif
;

gen system(cmd:string):int= {
  if ECHO==1 do fprintln$ cerr, cmd; done
  var result = System::system(cmd);
  if ECHO==1 do fprintln$ cerr, "Result code " + str(result); done
  return result;
}

// Optionally print a debug line.
proc debugln[T with Str[T]] (x:T) {
  dbug ?? fprintln (cerr, str x);
}

var argno=1;
fun prefix(arg:string,key:string)=>
  arg.[to len key]==key
;

var compile_exts = List::list ('cpp','cxx','c','cc');
var linkexts =  List::list ('o','obj','lib','dll','a','so');
var arg = "";
var result = 0;

whilst grab == 1 and argno<System::argc do
  arg = System::argv argno;
  debugln$ "ARGNO="+str(argno)+", arg='"+arg+"'";
  var path,ext = splitext(arg);
  var dir,base = Filename::split1(path);
  debugln$ "path="+path+", ext="+ext+",dir="+dir+",base="+base;
  if ext != "flx" and ext != "" do
    // add to list of things to link, and also things to compile
    // if the extension is appropriate
    if List::mem eq of (string * string) compile_exts ext do
      cpps = cpps + " " + arg;
      cppos = cppos + " " + path + "." + EXT_OBJ;
    else
     cppos = cppos + " " + arg;
    done

  elif arg == "--nostdimport" do
    debugln "No standard library import";
    // Note: currently, Felix compiler generates code that REQUIRES
    // the standard library, eg the driver passes a gc_profile_t record
    // and the compiler generates _uctor_ objects, etc etc
    STDIMPORT="";

  elif prefix(arg,"--import=") do
   debugln "Add import";
   IMPORTS=IMPORTS + " " + arg.[9 to];

  elif prefix(arg,"--test=") do
    debugln "Set test directory";
    FLX_INSTALL_DIR=arg.[7 to];

  elif arg=="--test" do
    debugln "Set test directory";
    FLX_INSTALL_DIR=".";

  elif arg=="--install" do
    debugln "Install Felix";
    println "Install Felix: ONLY ON UNIX (you may need to be superuser)";
    println "Always installs the --test directory to the configured install target";
    println "Because that is hard coded into this program";
    println "Note: does NOT install this program 'flx' into your PATH!";
    println$ "FROM: " + FLX_INSTALL_DIR;
    println$ "TO  : " + INSTALL_ROOT;
    if FLX_INSTALL_DIR == INSTALL_ROOT do
      println "Can't install, src and dst are the same";
      System::exit(1);
    else
      result=system("mkdir -pv "+INSTALL_ROOT_TOPDIR);
      if result != 0 do
        println$ "Cannot create directory " + INSTALL_ROOT_TOPDIR;
        System::exit 1;
      done
      result=system("rm -rf "+INSTALL_ROOT);
      result=system("mkdir -pv "+INSTALL_ROOT);
      result=system("cp -Rpv "+FLX_INSTALL_DIR+"/* "+INSTALL_ROOT);
      if result == 0 do println "Install succeeded"
      else println$ "Install failed, code = " + str(result);
      done
      System::exit(result);
    done

  elif prefix(arg,"--stdout=") do
    debugln "Redirect standard output";
    // of the Felix program only: used for saving the output
    // to a file so the test harness can compare it with an .expect file
    STDOUT=arg.[9 to];

  elif arg=="--show" do
    SHOWCODE=1;

  elif arg=="--force" do
    debugln "Force recompilation";
    // of the felix code, runs Felix unless --nofelix is set
    // the C++ compiler is run unless the felix compile failed
    RECOMPILE=1;

  elif arg=="--debug" do
    debugln "Enable runtime debugging";
    DEBUG=1;

  elif arg=="--debug-compiler" do
    debugln "Enable compiler debugging";
    DEBUG_COMPILER=1;

  elif arg=="--nooptimise" do
    debugln "Disable optimisation";
    NOOPTIMISE=1;

  elif arg=="--nostdlib" do
    debugln "Do not load standard library";
    NOSTDLIB=1;

  elif arg == "--echo" do
    debugln "Echo commands sent to system";
    ECHO=1;

  elif arg == "--static" do
    debugln "Compile a statically linked program";
    STATIC=1;

  elif prefix(arg,"--inline=") do
    debugln "Set inline aggressiveness";
    INLINE=int(arg.[to 9]);

  elif arg == "--inline" do
    debugln "Set inline aggressiveness";
    INLINE=100;

  elif arg == "--noinline" do
    debugln "Disable inlining (NOT RECOMMENDED)";
    INLINE=0;

  elif arg == "--version" do
    debugln "Print Felix version and exit";
    print("version ");
    println(VERSION);
    System::exit(0);

  elif arg == "--config" do
    println$ "VERSION            = "+VERSION;
    println$ "INSTALL_ROOT_TOPDIR= "+INSTALL_ROOT_TOPDIR;
    println$ "INSTALL_ROOT       = "+INSTALL_ROOT;
    println$ "FLX_INSTALL_DIR    = "+FLX_INSTALL_DIR;
    println$ "";
    println$ "CYGWIN             = "+str CYGWIN;
    println$ "WIN32              = "+str WIN32;
    println$ "MACOSX             = "+str MACOSX;

    println$ "";
    println$ "HAVE_GNU           = "+str HAVE_GNU;
    println$ "HAVE_MSVC          = "+str HAVE_MSVC;

    println$ "";
    println$ "CCOBJ_DLLIB        = "+CCOBJ_DLLIB;
    println$ "CCLINK_DLLIB       = "+CCLINK_DLLIB;
    println$ "CCOBJ_STATIC_LIB   = "+CCOBJ_STATIC_LIB;
    println$ "CCLINK_STATIC      = "+CCLINK_STATIC;

    println$ "";
    println$ "EXT_LIB            = "+EXT_LIB;
    println$ "EXT_STATIC_OBJ     = "+EXT_STATIC_OBJ;
    println$ "EXT_SHARED_OBJ     = "+EXT_SHARED_OBJ;
    println$ "EXT_EXE            = "+EXT_EXE;
    println$ "EXT_SHLIB          = "+EXT_SHLIB;
    println$ "SPEC_OBJ_FILENAME  = "+SPEC_OBJ_FILENAME;
    println$ "SPEC_EXE_FILENAME  = "+SPEC_EXE_FILENAME;

    println$ "";
    println$ "OPTIMISE           = "+str OPTIMISE;
    println$ "DEBUG_FLAGS        = "+str DEBUG_FLAGS;
    System::exit(0);

  elif arg == "--options" do
    println$ "NOOPTIMISE         = "+str NOOPTIMISE;
    println$ "STATIC             = "+str STATIC;
    println$ "ECHO               = "+str ECHO;
    println$ "NOSTDLIB           = "+str NOSTDLIB;
    println$ "DEBUG              = "+str DEBUG;
    println$ "DEBUG_COMPILER     = "+str DEBUG_COMPILER;
    println$ "STDIMPORT          = "+str STDIMPORT;
    println$ "IMPORTS            = "+str IMPORTS;
    println$ "RECOMPILE          = "+str RECOMPILE;
    println$ "cpps               = "+str cpps;
    println$ "cppos              = "+str cppos;
    println$ "TIME               = "+str TIME;
    println$ "OUTPUT_DIR         = "+str OUTPUT_DIR;
    println$ "CACHE_DIR          = "+str CACHE_DIR;
    println$ "RUNIT              = "+str RUNIT;
    println$ "INCLUDE_DIRS       = "+str INCLUDE_DIRS;
    println$ "FELIX              = "+str FELIX;
    println$ "LINKER_SWITCHES    = "+str LINKER_SWITCHES;
    println$ "MACROS             = "+str MACROS;
    println$ "SHOWCODE           = "+str SHOWCODE;
    System::exit(0)

  elif arg == "--where" do
    debugln "Print location of install directory and exit";
    println(FLX_INSTALL_DIR);
    System::exit(0);

  elif arg == "--time" do
    debugln "Time program execution and print after running";
    TIME=1;

  elif prefix(arg,"--output_dir=") do
    debugln "Set the directory for compiler generated C++ files";
    OUTPUT_DIR=arg.[13 to];

  elif prefix(arg,"--cache_dir=") do
    debugln "Set the directory for compiler generated *.par files";
    CACHE_DIR=arg.[12 to];

  elif arg == "--man" do
    debugln "Display top level manual page using 'man' program";
    C_hack::ignore(system( "man -M "+FLX_INSTALL_DIR+Plat_Filename::sep+"man"+" flx"));
    System::exit(0);

  elif arg == "--help" do
    debugln "Display top level manual page using 'man' program";
    println "Usage: flx [options] filename[.flx] [args ..]";
    println "options:";
    println "--test               : use felix installation in current directory";
    println "--test=dir           : use felix installation in dir";
    println "--where              : print location of felix installation";
    println "--show               : print the felix program to stdout";
    println "-c                   : compile only, do not run";
    println "--static             : make standalone statically linked executable";
    println "--nofelix            : do not run felix translator, leave C++ outputs alone";
    println "--options            : show option set";
    println "--config             : show configuration";
    println "--version            : show felix version";
    println "--force              : force run Felix compiler";
    println "--cache_dir=dir      : extant directory cache output from parser (*.par files)";
    println "--output_dir=dir     : extant directory to hold C++ output from translator";
    println "--man                : show flx man page (if there is one)";
    println "--help               : show this help";
    println "--noinline           : force inlining off, may break things!";
    println "--inline             : aggressive inlining"; 
    println "--inline=999         : set inline cap to 999 'instructions'"; 
    println "--echo               : print shell commands before running them";
    println "--time               : print target program run time after it finishes";
    println "--nostdlib           : don't load the standard library";
    println "--nooptimise         : disable C++ compiler optimisation";
    println "--debug              : put debug symbols in generated binaries";
    println "--debug-compiler     : make felix compiler print progress diagnostics";
    println "--stdout=file        : run program with standard output redirected to file";
    println "--install            : install --test directory to configured location";
    println "--import=file        : add an import which is prefixed to all files being translated";
    println "--nostdimport        : don't import the standard imports nugram.flxh and flx.flxh";
    println "-Idir                : add dir to search path for both felix and C++ includes";                      
    println "-Ldir                : add dir to linker search path"; 
    println "-llib                : add dir lib to linker command";
    println "-Dmac                : add macro def to C++ compiler command";
    println "-DFLX_ENABLE_TRACE       : enable compilation of trace generators (defaults off)";
    println "-DFLX_PTF_STATIC_STRUCT  : make the thread_frame a static object (default Felix heap)";
    println "-DFLX_PTF_STATIC_POINTER : access the thread_frame from a static pointer (default pass as arg)";
    println "-DFLX_CGOTO              : use gcc indirect gotos and use assembler hack for long jumps (default on if config detects support)";
    println "";
    println "*.c *.cc *.cpp *.cxx ";
    println "                     : add files to C++ compilation (and linker) steps";
    println "*.o *.obj *.lib *.dll *.a *.so";
    println "                     : add files to linker steps";
    println "* *.flx              : Felix program name, terminates options and starts runtime arguments";
    System::exit(0);

  elif arg == "-c" do
    debugln "Compile program but do not run it";
    RUNIT=0;

  elif prefix(arg,"-I") do
    debugln "Set include directories for both Felix and C/C++";
    INCLUDE_DIRS=INCLUDE_DIRS + " " + arg;

  elif arg== "--nofelix" do
    debugln "Do not translate Felix code, just compile generated C++ (used to debug at C++ level)";
    FELIX=0;

  elif prefix(arg,"-l") or prefix(arg,"-L") do
    debugln "Set extra switched for linker";
    LINKER_SWITCHES=LINKER_SWITCHES + " " + arg;

  elif prefix(arg,"-D") do
    debugln "Set extra macros for C++ compilation";
    MACROS=MACROS + " " + arg;

  elif prefix(arg,"--pkg=") do
    debugln "Add pkgconfig package to link";
    pkgs= pkgs + " " + arg.[6 to];

  elif prefix(arg,"--") do
    debugln "Unknown -- style option, abort";
    println("Unknown option '"+ arg+"'");
    System::exit(1);

// the main filename -- subsequent args are args to flx_run
  else
    debugln "Assume we have the filename now";
    grab=0;
  done
  argno = argno + 1;
done

debugln$ grab, argno, System::argc;
if grab == 1 and argno == System::argc do
  println "usage: flx [options] filename";
  System::exit(1);
done

debugln "#--------";
debugln$ "DONE, option index = "+str(argno);
debugln$ "path="+path+": dir="+dir+",base="+base", ext="+ext;
debugln$ "cpps="+cpps;
debugln$ "cppos="+cppos;

var USER_ARGS = ""; whilst argno < System::argc do USER_ARGS+=" " + System::argv argno; ++argno; done
//println$ "USER_ARGS=" + USER_ARGS;

if NOOPTIMISE == 0 do
  debugln "Set C++ compiler optimisation switches";
  CCFLAGS=CCFLAGS+" " + OPTIMISE;
else
  debugln "What, no optimisation?";
done

var PKGCONFIG=Filename::join$ List::list(FLX_INSTALL_DIR,"bin","flx_pkgconfig");
debugln$ "Felix package manager program is "+PKGCONFIG;

if ECHO == 1 do
  fprintln (cerr, "#FLX_INSTALL_DIR="+FLX_INSTALL_DIR);
  fprintln (cerr, "#PKGCONFIG="+PKGCONFIG);
done

DLINK_STRING, SLINK_STRING = Config::link_strings(FLX_INSTALL_DIR);

CONFIG_DIR = Filename::join(FLX_INSTALL_DIR,'config');
debugln$ "Felix package manager config directory is "+CONFIG_DIR;
// make a list of any *.cpp files (or other g++ options ..)

var EXT_OBJ = 
  if STATIC == 0 then EXT_SHARED_OBJ
  else EXT_STATIC_OBJ
  endif
;

// this hack forces a directory name, because executing "prog"
// can fail if the currect directory is not on the PATH, 
// or worse, the wrong program can execute. The PATH is not
// searched if the filename includes a / somewhere so force one in.
// similarly for dynamic loaders looking for shared libraries
//
// It would probably be better to convert any relative filename
// to an absolute one, however this only makes sense on Unix 
// since Windows has multiple "drives" it is much harder to
// do the conversion.
dir = 
  if dir != "" then dir 
  else "."
  endif
;

var filebase = Filename::join(dir,base);
debugln$ "User program base is " + filebase;
var cpp_filebase = if OUTPUT_DIR=="" then filebase else Filename::join(OUTPUT_DIR,base) endif;
debugln$ "C++ file base is " + cpp_filebase;

// Find absolute pathname

if path == "" do
  fprint$ cerr, ("No such felix program: "+path+"\n");
  System::exit(1);
done

gen get_stdout(x:string) = {
  // We're screwed if popen doesn't work ..
/*
  if os.name == "nt": // popen doesn't work on Windows
    result := system(x + " >tmp.out");
    Stdout::flush();
    output := Text_file::load "tmp.out";
  else:
*/
    var fout = Process::popen_in(x+" 2>&1 ");
    if Text_file::valid fout do
      var output = Text_file::load fout;
      var result = Process::pclose fout; 
      return (Process::WEXITSTATUS result), output;
    else
      println$ "Unable to run command '" + x "'";
      return -1,"";
    done
}

gen xqt(cmd:string) = {
  ECHO == 1 or dbug ?? fprintln (cerr, "cmd="+cmd);
  var result,output = get_stdout(cmd);
  if result == 0 do
    n := 
      match find_first_of (output, char "\n") with
      | Some ?n => n 
      | None => len output 
      endmatch
    ; 
    output = output.[to n]; // first line excluding newline
    ECHO==1 or dbug ?? fprintln (cerr, "output='"+output+"'");
  else
    debugln "COMMAND FAILED";
    fprint$ cerr, ("Error "+repr(result)+" executing command " + cmd + "/n");
    System::exit result;
  done
  return output;
}

proc calpackages () {

  // find external header files
  var PKGCONFIG_CFLAGS=PKGCONFIG+ " --path+="+Filename::join(FLX_INSTALL_DIR,"config") + " --field=cflags ";

  // external header files
  var PKGCONFIG_INCLUDES=PKGCONFIG+ " --path+="+Filename::join(FLX_INSTALL_DIR,"config") + " --field=includes ";

  // find external dynload libraries
  var PKGCONFIG_DLIBS=PKGCONFIG+" -r --path+="+Filename::join(FLX_INSTALL_DIR,"config") + " --field=provides_dlib --field=requires_dlibs ";

  // find external static libraries
  var PKGCONFIG_SLIBS=PKGCONFIG+" -r --keeprightmost --path+="+Filename::join(FLX_INSTALL_DIR,"config") + " --field=provides_slib --field=requires_slibs ";

  //find driver package required
  var PKGCONFIG_DRIVER=PKGCONFIG+" --path+="+Filename::join(FLX_INSTALL_DIR,"config") + " --field=flx_requires_driver ";


  // find all include directories
  var x = xqt(PKGCONFIG_CFLAGS+ " @"+cpp_filebase+".resh");
  INCLUDE_DIRS=INCLUDE_DIRS +" " + x;

  // find all include files
  x = xqt(PKGCONFIG_INCLUDES+ " @"+cpp_filebase+".resh");
  INCLUDE_FILES=INCLUDE_FILES +" " + x;

  // find the driver package
  DRIVER_PKG=xqt(PKGCONFIG_DRIVER+" @"+cpp_filebase+".resh");
  DRIVER_PKG == "" ??  DRIVER_PKG="flx_run";

  // find the driver entity
  if STATIC == 0 do
    // dynamic linkage: the driver executable
    DRIVER=Filename::join$ List::list (FLX_INSTALL_DIR,"bin",DRIVER_PKG+EXT_EXE);
  else
    // static linkage: the driver object file
    DRIVER=Filename::join$ List::list (FLX_INSTALL_DIR,"lib","rtl",DRIVER_PKG+EXT_STATIC_OBJ);
  done

  if STATIC == 0 do
    // dynamic linkage: all the libraries required by the application
    LINK_STRING=DLINK_STRING+LINKER_SWITCHES+xqt(
      PKGCONFIG_DLIBS+" @"+cpp_filebase+".resh");
  else
    // static linkage: all the libraries required by the application and driver
    LINK_STRING=SLINK_STRING+LINKER_SWITCHES+xqt(
      PKGCONFIG_SLIBS+" "+DRIVER_PKG+" @"+cpp_filebase+".resh");
  done

  if ECHO == 1 do
    fprintln (cerr, "//RESOURCE FILE="+cpp_filebase+".resh");
    fprintln (cerr, "//INCLUDE_DIRS="+INCLUDE_DIRS);
    fprintln (cerr, "//INCLUDE_FILES="+INCLUDE_FILES);
    fprintln (cerr, "//DRIVER="+DRIVER);
    fprintln (cerr, "//LINK_STRING="+LINK_STRING);
  done
}

proc write_include_file(path:string) {
  includes := split (strip INCLUDE_FILES,char " ");
  var f = Text_file::fopen_output(path+".includes");
  List::iter  
    (proc (i:string) { Text_file::writeln$ f, "#include " + i; })
    includes
  ;
  Text_file::fclose f;
}

val args = USER_ARGS;
debugln$ "Target program args = "+args;

var INCLUDE_DIR="-I"+Filename::join(FLX_INSTALL_DIR,"lib","rtl") + " -I"+Filename::join(FLX_INSTALL_DIR,"config","target");
debugln$ "C++ Include directory for Felix library " + INCLUDE_DIR;

var FLXLIB="-I"+Filename::join(FLX_INSTALL_DIR,"lib");
var STDLIB="std";
if NOSTDLIB == 1 do
  FLXLIB="";
  STDLIB="";
done

if WIN32 do
  FLXG=Filename::join(FLX_INSTALL_DIR, 'bin', 'flxg');
  FLXRUN='';
else
  FLXG="env PATH="+FLX_INSTALL_DIR+"/bin:$PATH "+FLX_INSTALL_DIR+"/bin/flxg";
  // the mac uses DYLD_LIBRARY_PATH instead of LD_LIBRARY_PATH
  if MACOSX do
    FLXRUN="env DYLD_LIBRARY_PATH="+FLX_INSTALL_DIR+"/lib/rtl:$DYLD_LIBRARY_PATH ";
  else
    FLXRUN="env LD_LIBRARY_PATH="+FLX_INSTALL_DIR+"/lib/rtl:$LD_LIBRARY_PATH ";
  done
done

var DEBUGSWITCH="";
DEBUG_COMPILER == 1?? DEBUGSWITCH=" --debug";

var STATIC_ENV="";
DEBUG_COMPILER == 1?? STATIC_ENV="env FLX_DEBUG=1 ";

body """
long macosx_ftime(string s) {
  struct stat sb;
  int e = stat(s.data(),&sb);
  if(e != 0) return 0l;
  return sb.st_mtime;
}
""" requires header "#include <sys/stat.h>";

body """
long posix_ftime(string s) {
  struct stat sb;
  int e = stat(s.data(),&sb);
  if(e != 0) return 0l;
  return sb.st_mtime;
}
""" requires header "#include <sys/stat.h>";


fun macosx_ftime: string -> long = "macosx_ftime($1)";
fun posix_ftime: string -> long = "posix_ftime($1)";

val past_time = -1l;
val future_time = long(ulong(-1)/2ul); // a hacky way to get a big number

fun filetime(f:string)=>if MACOSX then macosx_ftime(f) else posix_ftime(f) endif;
fun dfiletime(f:string, dflt:long )=
{
  val x = filetime (f);
  dbug??println$ "Time of file " + f + " is " + str x;
  return if x == 0l then dflt else x endif;
}

fun maxf (x: long) (f:string) => max (x, if f == "" then x else dfiletime (f+".flx",future_time) endif);
fun cal_files_time (fs: list[string])=> fold_left maxf 0L fs;
fun cal_time_from_dep (df: string):long=
{
  val deptext = Text_file::load df;
  val lines = split (deptext, "\n"); 
  debugln$ "Deps=" + str(lines);
  val deptime = cal_files_time lines;
  debugln$ "Deptime=" + str(deptime);
  return deptime;
}

fun cal_dep_time(f:string)=>cal_time_from_dep(f+".dep");

var cmd = "";
debugln$ "RECOMPILE="+str RECOMPILE;
debugln$ "RUNIT="+str RUNIT;
if SHOWCODE == 1 do {
  prg := Text_file::load(filebase+".flx");
  print prg;
} done

if RECOMPILE == 0 and RUNIT == 1 do
  // not (--force or -c)
  debugln "Checking to see if the binary is uptodate";
  debugln$ "Binary time = " + str (dfiletime(filebase+EXT_SHLIB,past_time));
  if STATIC == 0 do
    if (dfiletime(filebase+EXT_SHLIB,past_time) > cal_dep_time (filebase)) do
      debugln$ "Running dynamically linked binary";
      calpackages();
      cmd=FLXRUN+DRIVER+DEBUGSWITCH+" "+ filebase+EXT_SHLIB+" "+args;
      debugln$ "Uptodate shared lib: Running command " + cmd;
      System::exit(system(cmd));
    else
      debugln$ "Dynamically linked binary out of date or non-existant";
    done
  else
    if (dfiletime(filebase+EXT_EXE,past_time) > cal_dep_time(filebase)) do
      debugln$ "Running statically linked binary";
      cmd=STATIC_ENV+" "+filebase+" "+args;
      debugln$ "Uptodate executable: Running command " + cmd;
      System::exit(system(cmd));
    else
      debugln$ "Statically linked binary out of date or non-existant";
    done
  done
done

// Need Felix and c++ compile, then run it
dbug??println$ "RECOMPILING!!";

var VERBOSE = "";
if DEBUG_COMPILER == 1 do
  VERBOSE="-v";
  debugln "Compiler debugging on";
else
  VERBOSE="-q";
  debugln "Compiler debugging off";
done

if DEBUG==1 do
  CCFLAGS=CCFLAGS+DEBUG_FLAGS;
done

var FLXFLAGS="--inline="+str(INLINE);
if OUTPUT_DIR != "" do FLXFLAGS += ' --output_dir=' + str(OUTPUT_DIR); done;
if CACHE_DIR != "" do FLXFLAGS += " --cache_dir=" + CACHE_DIR; done

var FCMD="";
var LCMD="";
var CCMD="";

if FELIX == 1 do
  FCMD=List::cat ' ' (List::list (
    FLXG,
    VERBOSE, FLXFLAGS, FLXLIB,
    INCLUDE_DIRS, STDIMPORT, IMPORTS,
    STDLIB, filebase));
  debugln$ "Felix command="+FCMD;
  result=system(FCMD);
  if result != 0 do 
   debugln "Felix compilation failed";
    System::exit(int(0!=result)); 
  done
  debugln "Felix compilation succeeded";
  calpackages();
  write_include_file(cpp_filebase);
else
  debugln "Felix compilation skipped by switch";
  calpackages(); // need to do this here to set linker switches
done


if STATIC == 0 do
  debugln "Dynamic linkage";
  CCMD=List::cat ' ' (List::list (
    CCOBJ_DLLIB, CCFLAGS, "-DTARGET_BUILD",
    INCLUDE_DIR, INCLUDE_DIRS, MACROS,
    cpps, cpp_filebase+".cpp",
    SPEC_OBJ_FILENAME+cpp_filebase+EXT_OBJ));
  LCMD=List::cat ' ' (List::list (
    CCLINK_DLLIB, CCFLAGS,
    cppos, cpp_filebase+EXT_OBJ,
    SPEC_EXE_FILENAME+filebase+EXT_SHLIB,
    LINK_STRING));
  debugln$ "C++ command="+CCMD;
  result = system(CCMD);
  if result == 0 do
    debugln$ "Link command="+LCMD;
    result = system(LCMD);
    if result == 0 do
      if RUNIT == 1 do
        if TIME == 1 do
          cmd=List::cat ' ' (List::list (
            TIMECMD,
            FLXRUN+DRIVER+DEBUGSWITCH,
            filebase+EXT_SHLIB, args));
        else
          cmd=List::cat ' ' (List::list (
            FLXRUN+DRIVER+DEBUGSWITCH,
            filebase+EXT_SHLIB, args));
        done
        if STDOUT != "" do cmd=cmd+" > " +STDOUT; done
        debugln$ "Run command="+cmd;
        System::exit(int(0!=system(cmd)));
      else
        debugln "Not running program selected by switch";
      done
    else
      debugln "Dynamic linkage failed";
    done
  else
    debugln "C++ compilation failed";
  done
else
  debugln "Static linkage";
  CCMD=List::cat ' ' (List::list (
    CCOBJ_STATIC_LIB,
    CCFLAGS, "-DTARGET_BUILD",
    "-DFLX_STATIC_LINK", INCLUDE_DIR, INCLUDE_DIRS,
    MACROS, cpps, cpp_filebase+".cpp",
    SPEC_OBJ_FILENAME+cpp_filebase+EXT_OBJ));
  LCMD=List::cat ' ' (List::list (
    CCLINK_STATIC, SPEC_EXE_FILENAME+filebase+EXT_EXE,
    cpp_filebase+EXT_OBJ, DRIVER, cppos, LINK_STRING));
  debugln$ "C++ command="+CCMD;
  result=system(CCMD);
  if result == 0 do
    debugln$ "Link command="+LCMD;
    result=system(LCMD);
    if result == 0 do
      // rm -f "$base.cpp"
      if RUNIT == 1 do
        if TIME == 1 do
          cmd= List::cat ' ' (List::list (
            TIMECMD, STATIC_ENV, filebase, args));
        else
          cmd=List::cat ' ' (List::list (
            STATIC_ENV, filebase,args));
        done
        if STDOUT != "" do cmd=cmd + " > "+STDOUT; done
        debugln$ "Run command="+cmd;
        System::exit(int(0!=system(cmd)));
      else
        debugln "Not running program selected by switch";
      done
    else
      debugln "Static Linkage failed";
    done
  else
    debugln "C++ compilation failed";
  done
done

System::exit(int(0!=result));


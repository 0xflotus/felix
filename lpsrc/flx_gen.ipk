@head(1,'GC shape object generator')
@h = tangler('src/flx_display.mli')
@select(h)
open Flx_types
open Flx_ast
val get_display_list:
  fully_bound_symbol_table_t ->
  bid_t ->
  (bid_t * int) list


@h = tangler('src/flx_display.ml')
@select(h)
open Flx_ast
open Flx_types
open Flx_util

let rec cal_display bbdfns parent display : bid_t list =
  match parent with 
  | None -> display
  | Some parent ->
  match 
    try Hashtbl.find bbdfns parent 
    with Not_found -> 
      failwith ("[cal_display] Can't find index " ^ si parent)
  with
  | _,parent',sr,`BBDCL_procedure _
  | _,parent',sr,`BBDCL_function _
    -> cal_display bbdfns parent' (parent::display)
  | _ -> assert false

let get_display_list' bbdfns index : bid_t list =
  match 
    try Hashtbl.find bbdfns index 
    with Not_found -> failwith ("[get_display_list] Can't find index " ^ si index)
  with
  | id,parent,sr,`BBDCL_procedure _
  | id,parent,sr,`BBDCL_function _ ->
    List.rev (cal_display bbdfns parent [])
  | _ -> assert false

let get_display_list bbdfns index : (bid_t * int) list =
  List.map
  (fun i -> match Hashtbl.find bbdfns i with
  | _,_,_,`BBDCL_procedure (_,vs,_,_) -> i,List.length vs
  | _,_,_,`BBDCL_function (_,vs,_,_,_) -> i,List.length vs
  | _ -> assert false
  )
  (get_display_list' bbdfns index)

@h = tangler('src/flx_ogen.mli')
@select(h)
open Flx_ast
open Flx_types
open Flx_mtypes1

val gen_offset_tables:
  sym_state_t ->
  (bid_t, bid_t list) Hashtbl.t *
  fully_bound_symbol_table_t ->
  string

val find_thread_vars_with_type:
  fully_bound_symbol_table_t ->
  (bid_t * btypecode_t) list

val find_references:
  sym_state_t ->
  (bid_t, bid_t list) Hashtbl.t *
  fully_bound_symbol_table_t ->
  bid_t ->
  btypecode_t list ->
  (bid_t * btypecode_t) list

@h = tangler('src/flx_ogen.ml')
@select(h)
open Flx_util
open Flx_ast
open Flx_types
open Flx_mtypes1
open Flx_name
open Flx_unify
open Flx_typing
open Flx_tgen
open List
open Flx_print

let find_thread_vars_with_type bbdfns =
  let vars = ref [] in
  Hashtbl.iter
  (fun k (id,parent,sr,entry) ->
    match parent,entry with
    | None,`BBDCL_var (_,t)
    | None,`BBDCL_val (_,t)
      -> vars := (k,t) :: !vars
    | _ -> ()
  )
  bbdfns
  ;
  !vars


let find_references syms (child_map,bbdfns) index ts = 
  let children = 
    try 
      Hashtbl.find child_map index 
    with Not_found -> []
  in
  let references = ref [] in
  iter
  (fun idx -> 
    try
      let id,_,_,bbdfn = 
        Hashtbl.find bbdfns idx
      in
      match bbdfn with
      | `BBDCL_var (vs,(`BTYP_pointer _ as t))
      | `BBDCL_val (vs,(`BTYP_pointer _ as t))
      | `BBDCL_var (vs,(`BTYP_function _ as t))
      | `BBDCL_val (vs,(`BTYP_function _ as t))
        -> 
        if length ts <> length vs then 
        failwith 
        (
          "[find_references} wrong number of args, expected vs = " ^ 
          si (length vs) ^ 
          ", got ts=" ^ 
          si (length ts)
        );
        let t = tsubst vs ts (lower t) in
        references := (idx,t) :: !references
      | _ -> ()
    with Not_found -> ()
  ) 
  children 
  ;
  !references

let comma_sub s = 
  let rec aux l r =
    try  
      let i = String.index r ',' in
      let n = String.length r in
      aux (l ^ String.sub r 0 i ^ " comma ") (String.sub r (i+1) (n-i-1))
    with Not_found -> l ^ r
  in
  aux "" s
 
(* this code handles tuples and pointers *)
let rec get_offsets syms bbdfns typ : string list =
  let tname = cpp_typename syms typ in
  match unfold typ with
  | `BTYP_pointer t -> 
    ["offsetof("^tname^",frame)"]

  | `BTYP_tuple args -> 
    let n = ref 0 in
    let lst = ref [] in
    iter 
    (fun t ->
      let prefix = 
        "offsetof("^tname^",mem_"^si !n^")+" 
      in
      iter
      (fun s -> lst := !lst @ [prefix ^ s])
      (get_offsets syms bbdfns t)
      ;
      incr n
    )
    args
    ;
    !lst

  | _ -> []

let gen_offset_data s n name offsets =
  if n <> 0 then
  begin
    bcat s ("static std::size_t " ^ name ^ "_offsets["^si n^"]={\n");
    bcat s ("  " ^ cat ",\n  " offsets);
    bcat s ("\n" ^  "};\n");
  end;
  bcat s ("static void " ^ name ^ "_finaliser(collector_t *, void *p){\n");
  bcat s ("  (("^ name ^ "*)p)->~" ^ name ^ "();\n}\n");
  bcat s (  "static gc_shape_t "^ name ^"_ptr_map (\n");
  bcat s ( "  1,sizeof("^name^"),\n  "^name^"_finaliser,\n  "^si n^",\n  "^
  (if n<>0 then name^"_offsets\n" else "0\n"));
  bcat s ( ");\n")

let gen_fun_offsets s syms (child_map,bbdfns) index vs ps ret ts instance = 
  let vars =  (find_references syms (child_map,bbdfns) index ts) in
  let refs = filter 
    (fun (i,t) -> match t with 
    | `BTYP_pointer _ -> true 
    | _ -> false
    ) vars 
  in
  let funs = filter 
    (fun (i,t) -> match t with 
    | `BTYP_function _ -> true 
    | _ -> false
    ) vars 
  in
  let refnames = 
    map 
    (fun (index,t)-> 
      cpp_instance_name syms bbdfns index ts,
      cpp_typename syms t
    )
    refs
  in
  let funnames = 
    map 
    (fun (index,t)-> 
      cpp_instance_name syms bbdfns index ts,
      cpp_typename syms t
    )
    funs
  in
  let name = cpp_instance_name syms bbdfns index ts in
  let display = Flx_display.get_display_list bbdfns index in
  let offsets = 
    (match ret with 
      | `BTYP_void -> [ ("offsetof(" ^ name ^ ",_caller)")  ] 
      | _ -> []
    ) 
    @
    map
    (fun (didx, vslen) ->
    let dptr = "ptr" ^ cpp_instance_name syms bbdfns didx (list_prefix ts vslen) in
    "offsetof("^name^","^dptr^")"
    )
    display
    @
    map 
    (fun (mem,reftyp)->
      "offsetof("^name^","^mem^")" ^
      "+offsetof("^comma_sub reftyp^",frame)") 
    refnames
    @
    map 
    (fun (mem,reftyp)->
      "offsetof("^name^","^mem^")"
    )
    funnames
  in
  let n = length offsets in
  bcat s 
  (
    "\n//OFFSETS for "^
    (match ret with |`BTYP_void -> "procedure " | _ -> "function ") ^ 
    name ^ "\n"
  );
  gen_offset_data s n name offsets

let gen_thread_frame_offsets s syms bbdfns =
  let vars = find_thread_vars_with_type bbdfns in
  let ts = [] in
  let refs = filter 
    (fun (i,t) -> match t with 
    | `BTYP_pointer _ -> true 
    | _ -> false
    ) vars 
  in
  let funs = filter 
    (fun (i,t) -> match t with 
    | `BTYP_function _ -> true 
    | _ -> false
    ) vars 
  in
  let refnames = 
    map 
    (fun (index,t)-> 
      cpp_instance_name syms bbdfns index ts,
      cpp_typename syms t
    )
    refs
  in
  let funnames = 
    map 
    (fun (index,t)-> 
      cpp_instance_name syms bbdfns index ts,
      cpp_typename syms t
    )
    funs
  in
  let name = "thread_frame_t" in
  let offsets = 
    map
    (fun (mem,reftyp)->
      "offsetof("^name^","^mem^")" ^
      "+offsetof("^comma_sub reftyp^",frame)" 
    )
    refnames
    @
    map 
    (fun (mem,reftyp)->
      "offsetof("^name^","^mem^")"
    )
    funnames
  in
  let n = length offsets in
  bcat s 
  (
    "\n//OFFSETS for "^ name ^ "\n"
  );
  gen_offset_data s n name offsets

let gen_offset_tables syms (child_map,bbdfns) =
  let primitive_shapes = Hashtbl.create 97 in
  let s = Buffer.create 20000 in

  (* print_endline "Function and procedure offsets"; *)
  Hashtbl.iter
  (fun (index,ts) instance ->
  let id,parent,sr,entry = Hashtbl.find bbdfns index in 
  (*
  print_endline ("Offsets for " ^ id ^ "<"^ si index ^">["^catmap "," (sbt syms.dfns) ts ^"]");
  *)
  match entry with
  | `BBDCL_function (props,vs,ps, ret,_) -> 
    gen_fun_offsets s syms (child_map,bbdfns) index vs ps ret ts instance
      
  | `BBDCL_procedure (props,vs,ps,_) -> 
    gen_fun_offsets s syms (child_map,bbdfns) index vs ps `BTYP_void ts instance
  | _ -> ()
  )
  syms.instances
  ;
  gen_thread_frame_offsets s syms bbdfns
  ;

  (* We're not finished: we need offsets for tuple and pointer types too *)
  (* print_endline "Tuple and pointer offsets"; *)
  Hashtbl.iter
  (fun btyp index -> 
    (*
    print_endline ("type --> " ^ string_of_btypecode syms.dfns btyp);
    *)
    match unfold btyp with
    | `BTYP_tuple args ->
      let name = cpp_type_classname syms btyp in
      let offsets = get_offsets syms bbdfns btyp in
      let n = length offsets in 
      let classname = cpp_type_classname syms btyp in
      bcat s ("\n//OFFSETS for tuple type " ^ si index ^ "\n");
      gen_offset_data s n name offsets

    (* for an array, we only have offsets for the first element *)
    | `BTYP_array (t,i) ->
      let k = 
        try int_of_unitsum i
        with Not_found -> failwith "Array index must be unitsum"
      in
      let name = cpp_typename syms btyp in
      let tname = cpp_typename syms t in
      let offsets = get_offsets syms bbdfns t in
      let n = length offsets in 
      bcat s ("\n//OFFSETS for array type " ^ si index ^ "\n");
      if n <> 0 then begin
        bcat s ("static std::size_t " ^ name ^ "_offsets["^si n^"]={\n  ");
        bcat s ("  " ^ cat ",\n  " offsets);
        bcat s "};\n"
      end
      ;
      bcat s ("static void " ^ name ^ "_finaliser(collector_t *, void *p){\n");
      bcat s ("  for(std::size_t count = "^ si k ^"; count; --count)\n");
      bcat s ("  {\n");
      bcat s ("    (("^ tname ^ "*)p)->~" ^ tname ^ "();\n");
      bcat s ("    p = (void*)((char*)p + sizeof("^tname^"));\n");
      bcat s ("  }\n");
      bcat s ("}\n");
      bcat s ("static gc_shape_t "^ name ^"_ptr_map(\n");
      bcat s ("  " ^ si k ^ ",\n");
      bcat s ("  sizeof("^name^"),\n  "^name^"_finaliser,\n  "^si n^
      (
        if n = 0 then ",0\n" 
        else ",\n  " ^name^"_offsets\n"
      )
      );
      bcat s ");\n"

    | `BTYP_inst (i,ts) ->
      let name = cpp_typename syms btyp in
      let id,parent,sr,entry = Hashtbl.find bbdfns i in 
      begin match entry with
      | `BBDCL_abs (_,complete,_,_) ->
        if complete then
          if not (Hashtbl.mem primitive_shapes name) then 
          begin
            Hashtbl.add primitive_shapes name true; 
            bcat s ("\n//OFFSETS for abstract type " ^ name ^ " instance\n");
            bcat s ( "static gc_shape_t " ^ name ^ "_ptr_map(\n") ;
            bcat s ("  1,sizeof("^name^"),0,0,0\n");
            bcat s ");\n"
          end else begin
            bcat s ("\n//OFFSETS for abstract type " ^ name ^ " instance\n");
            bcat s ("//Use "^name^"_ptr_map\n");
          end
        else ()
      | `BBDCL_cstruct (vs,cps)
      | `BBDCL_struct (vs,cps) ->
        bcat s ("\n//OFFSETS for struct type " ^ name ^ " instance\n");
        bcat s ("//CANT HANDLE YET!\n");
      | _ -> ()
    end

   | _ -> () 
  )
  syms.registry
  ;
  Buffer.contents s

@head(1,'C++ Code generator')
@h = tangler('src/flx_gen.mli')
@select(h)
open Flx_ast
open Flx_types
open Flx_mtypes1
open Flx_label

val gen_functions:
  sym_state_t ->
  (bid_t, bid_t list) Hashtbl.t *
  fully_bound_symbol_table_t ->
  string

val gen_execute_methods:
  sym_state_t ->
  (bid_t, bid_t list) Hashtbl.t *
  fully_bound_symbol_table_t ->
  label_map_t ->
  int ref ->
  out_channel ->
  unit

val find_members:
  sym_state_t ->
  (bid_t, bid_t list) Hashtbl.t *
  fully_bound_symbol_table_t ->
  int ->
  btypecode_t list ->
  string

val gen_biface_headers:
  sym_state_t ->
  fully_bound_symbol_table_t ->
  biface_t list ->
  string

val gen_biface_bodies:
  sym_state_t ->
  fully_bound_symbol_table_t ->
  biface_t list ->
  string
  
val format_vars:
  sym_state_t ->
  fully_bound_symbol_table_t ->
  bid_t list ->
  btypecode_t list ->
  string

@h = tangler('src/flx_gen.ml')
@select(h)
open Flx_util
open Flx_ast
open Flx_types
open Flx_mtypes1
open Flx_print
open Flx_typing
open Flx_name
open Flx_tgen
open Flx_unify
open Flx_csubst
open Flx_exceptions
open Flx_display
open List
open Flx_generic
open Flx_label
open Flx_unravel
open Flx_ogen

let string_of_string = Flx_string.c_quote_of_string

(* HACKERY: this assumes library dependent things:
  but we can't add literals in the library code :-(
*)
let csuffix_of_type s = match s with
  | "tiny" -> ""
  | "short" -> ""
  | "int" -> ""
  | "long" -> "l"
  | "vlong" -> "ll"
  | "utiny" -> "u"
  | "ushort" -> "u"
  | "uint" -> ""
  | "ulong" -> "lu"
  | "uvlong" -> "llu"
  | "int8" -> ""
  | "int16" -> ""
  | "int32" -> "l"
  | "int64" -> "ll"
  | "uint8" -> "u"
  | "uint16" -> "u"
  | "uint32" -> "lu"
  | "uint64" -> "llu"
  | "double" -> ""
  | "float" -> "f"
  | "ldouble" -> "l"
  | _ -> failwith ("[csuffix_of_type]: Unexpected Type " ^ s)

let cstring_of_literal e = match e with
  | `AST_int (s,i) -> (Big_int.string_of_big_int i)^csuffix_of_type s 
  | `AST_float (s,x) -> x ^ csuffix_of_type s
  | `AST_string s -> string_of_string s
  | `AST_cstring s -> string_of_string s
  | `AST_wstring s -> "L" ^ string_of_string s
  | `AST_ustring s -> "L" ^ string_of_string s

let find_variable_indices syms (child_map,bbdfns) index = 
  let children = 
    try 
      Hashtbl.find child_map index 
    with Not_found -> [] 
  in
  filter
  (fun i -> 
    try match Hashtbl.find bbdfns i with _,_,_,entry ->
      match entry with 
      | `BBDCL_var _ 
      | `BBDCL_val _ -> true
      | _ -> false
    with Not_found -> false
  )
  children

let get_variable_typename syms bbdfns i ts =
  let id,parent,sr,entry = Hashtbl.find bbdfns i in
  match entry with
  | `BBDCL_var (vs,t)
  | `BBDCL_val (vs,t) ->
    let t = lower t in
    if length ts <> length vs then 
    failwith 
    (
      "[get_variable_typename} wrong number of args, expected vs = " ^ 
      si (length vs) ^ 
      ", got ts=" ^ 
      si (length ts)
    );
    let t = tsubst vs ts t in
    cpp_typename syms t

  | _ -> 
    failwith "[get_variable_typename] Expected variable"

let format_vars syms bbdfns vars ts =
  catmap  ""
  (fun idx -> 
    let instname = 
      try Some (cpp_instance_name syms bbdfns idx ts) 
      with _ -> None
    in 
      match instname with
      | Some instname ->
        let typename = get_variable_typename syms bbdfns idx ts in
        "  " ^ typename ^ " " ^ instname ^ ";\n"
      | None -> "" (* ignore unused variables *)
  )
  vars
 
let find_members syms (child_map,bbdfns) index ts = 
  let variables = find_variable_indices syms (child_map,bbdfns) index in
  match format_vars syms bbdfns variables ts with
  | "" -> ""
  | x -> "  //variables\n" ^x

let typeof_bparams ps: btypecode_t  = 
  typeoflist 
  (map
  (fun (id,(ix,t)) ->t) 
  ps
  )

let get_type bbdfns index = 
  let id,parent,sr,entry = 
    try Hashtbl.find bbdfns index 
    with _ -> failwith ("[get_type] Can't find index " ^ si index)
  in
  match entry with
  | `BBDCL_function (props,vs,ps,ret,_) -> 
      `BTYP_function (typeof_bparams ps,ret)
  | `BBDCL_procedure (props,vs,ps,_) -> 
      `BTYP_function (typeof_bparams ps,`BTYP_void)
  | _ -> failwith "Only function and procedure types handles by get_type"


(* vs here is the (name,index) list of type variables *)
let gen_function syms (child_map,bbdfns) index id vs bps ret' ts instance_no =
  let ps = map (fun (id,(ix,t)) -> id,t) bps in
  if syms.compiler_options.print_flag then
  print_endline 
  (
    "//Generating function inst " ^ 
    si instance_no ^ "=" ^ 
    id ^ "<" ^si index^">" ^
    (
      if length ts = 0 then "" 
      else "[" ^ catmap "," (string_of_btypecode syms.dfns) ts ^ "]"
    )
  );
  let argtype = lower(typeof_bparams bps) in
  if length ts <> length vs then 
  failwith 
  (
    "[gen_function} wrong number of args, expected vs = " ^ 
    si (length vs) ^ 
    ", got ts=" ^ 
    si (length ts)
  );
  let argtype = tsubst vs ts argtype in
  let ret = lower ret' in
  let ret = tsubst vs ts ret in
  let funtype = fold (`BTYP_function (argtype, ret)) in

  let argtypename = cpp_typename syms argtype in
  let funtypename = 
    try 
      Some (cpp_type_classname syms funtype) 
    with _ -> None 
  in 
  let display = get_display_list bbdfns index in
  let frame_dcls = 
    "  FLX_FMEM_DECL\n"
  in
  let display_string = match display with
    | [] -> ""
    | display -> 
      cat "" 
      (
        map
        (fun (i, vslen) -> 
         try
         let instname = cpp_instance_name syms bbdfns i (list_prefix ts vslen) in
         "  " ^ instname ^ " *ptr" ^ instname ^ ";\n"
         with _ -> failwith "Can't cal display name"
         )
        display
      )
  and ctor_dcl name =  
    "  " ^name^
    "  (\n" ^
    cat ",\n"
      (
        "    FLX_FPAR_DECL " ::
        map 
        (
          fun (i,vslen) -> 
          let instname = cpp_instance_name syms bbdfns i (list_prefix ts vslen) in
          "    " ^ instname ^ "*"
        )
        display
      )^
      "\n  );\n" 
  and dtor_dcl name =
    "  ~" ^ name ^"();\n"
  in
  let members = find_members syms (child_map,bbdfns) index ts in
  match ret with
  | `BTYP_void ->
    let name = cpp_instance_name syms bbdfns index ts in
    let ctor = ctor_dcl name in
    let dtor = dtor_dcl name in
    "struct " ^ name ^
    (match funtypename with 
    | Some x -> ": "^x
    | None -> ": con_t"
    )
    ^
    " {\n" ^
    "  //os frames\n" ^
    frame_dcls ^
    "  //display\n" ^
    display_string ^ "\n" ^
    members ^
    "  //constructor\n" ^
    ctor ^
    "  //destructor\n" ^
    dtor_dcl name ^
    "  //clone\n" ^
    "  " ^name^"* clone()const;\n" ^
    "  //call\n" ^
    (if argtype = `BTYP_tuple [] 
    then
    "  con_t *call(con_t*);\n" 
    else
    "  con_t *call(con_t*,"^argtypename^" const &);\n"
    ) ^
    "  //resume\n" ^
    "  con_t *resume();\n"  ^
    "};\n"

  | _ ->
    let name = cpp_instance_name syms bbdfns index ts in
    let rettypename = cpp_typename syms ret in
    let ctor = ctor_dcl name in
    let dtor = dtor_dcl name in
    "struct " ^ name ^
    (match funtypename with 
    | Some x -> ": "^x
    | None -> ""
    )
    ^
    " {\n" ^
    "  //os frames\n" ^
    frame_dcls ^
    "  //display\n" ^
    display_string ^ "\n" ^
    members ^
    "  //constructor\n" ^
    ctor ^
    "  //destructor\n" ^
    dtor ^
    "  //clone\n" ^
    "  " ^name^"* clone()const;\n" ^
    "  //apply\n" ^
    "  "^rettypename^" apply(" ^
    (if argtype = `BTYP_tuple[] then ""
    else argtypename^" const &")^ 
    ");\n"  ^
    "};\n"


(* This code generates the class declarations *)
let gen_functions syms (child_map,bbdfns) = 
  let xxdfns = ref [] in
  Hashtbl.iter
  (fun x i -> 
    (* if proper_descendant syms.dfns parent then  *)
    xxdfns := (i,x) :: !xxdfns
  )
  syms.instances
  ;
  
  flush stdout;
  let s = Buffer.create 2000 in
  iter
  (fun (i,(index,ts)) ->
    let tss = 
      if length ts = 0 then "" else 
      "[" ^ catmap "," (string_of_btypecode syms.dfns) ts^ "]"
    in
    match Hashtbl.find bbdfns index with (id,parent,sr,entry) -> 
    match entry with
    | `BBDCL_function (props,vs,ps, ret, _) -> 
      bcat s ("\n//------------------------------\n");
      bcat s ("//FUNCTION " ^ qualified_name_of_index syms.dfns index ^ tss ^ "\n");
      bcat s 
      (gen_function syms (child_map,bbdfns) index id vs ps ret ts i)

    | `BBDCL_procedure (props,vs,ps,_) -> 
      bcat s ("\n//------------------------------\n");
      bcat s ("//PROC " ^ qualified_name_of_index syms.dfns index ^ tss ^ "\n");
      bcat s 
      (gen_function syms (child_map,bbdfns) index id vs ps `BTYP_void ts i)
    | _ -> () (* bcat s ("//SKIPPING " ^ id ^ "\n") *)
  )
  (sort compare !xxdfns)
  ;
  Buffer.contents s

let gen_ctor syms bbdfns name display funs ts =
  name^"::"^name^
  "\n  (\n" ^
  cat ",\n"
  (
    "    FLX_FPAR_DECL" ::
    map 
    (
      fun (i,vslen) -> 
        let instname = cpp_instance_name syms bbdfns i (list_prefix ts vslen) in
      "    " ^ instname ^ " *pptr" ^ instname
    )
    display
  )^
  "\n  ):\n" ^
  cat ",\n"
  (
    "  FLX_FMEM_INIT" ::
    map 
    (
      fun (i,vslen) -> let instname = cpp_instance_name syms bbdfns i (list_prefix ts vslen) in
      "  ptr" ^ instname ^ "(pptr"^instname^")"
    )
    display
    @
    map 
    (fun (index,t)-> 
      cpp_instance_name syms bbdfns index ts
      ^ "(0)"
    )
    funs
  ) ^ 
  " {}\n"

let gen_dtor syms bbdfns name display ts =
  name^"::~"^name^"(){}\n"

let is_closure_var bbdfns index =
  let var_type bbdfns index =
    let id,_,entry = 
      try Hashtbl.find bbdfns index
      with Not_found -> failwith ("[var_type] ]Can't get index " ^ si index)
    in match entry with 
    | `BBDCL_var (_,t) 
    | `BBDCL_val (_,t) -> lower t
    | _ -> failwith ("[var_type] expected "^id^" to be variable")
  in
  match var_type bbdfns index with
  | `BTYP_function _ -> true
  | _ -> false
  
let get_var_ref syms bbdfns this index ts : string =
  match 
    try Hashtbl.find bbdfns index 
    with _ -> failwith ("[get_var_ref] Can't find index " ^ si index)
  with (id,parent,sr,entry) ->
  (*
  print_endline ("get var ref for " ^ id ^ "<" ^ si index ^ ">["^catmap "," (string_of_btypecode syms.dfns) ts^"]");
  *)
  match entry with
  | `BBDCL_val (vs,t)
  | `BBDCL_var (vs,t) ->
    begin match parent with
    | None -> (* print_endline "No parent ...?"; *)
      "ptf->" ^ cpp_instance_name syms bbdfns index ts
    | Some i -> 
      (*
      print_endline ("Parent " ^ si i);
      *)
      (
        if i <> this 
        then "ptr" ^ cpp_instance_name syms bbdfns i ts ^ "->" 
        else ""
      ) ^
      cpp_instance_name syms bbdfns index ts
    end
  | _ -> failwith ("[get_var_ref(3)] Expected name "^id^" to be variable or value")

let get_var_frame syms bbdfns this index ts : string =
  match 
    try Hashtbl.find bbdfns index 
    with _ -> failwith ("[get_var_frame(1)] Can't find index " ^ si index)
  with (id,parent,sr,entry) -> 
  match entry with
  | `BBDCL_val (vs,t)
  | `BBDCL_var (vs,t) ->
    begin match parent with
    | None -> "0"
    | Some i ->
      if i <> this
      then "ptr" ^ cpp_instance_name syms bbdfns i ts
      else "this"
    end
  | _ -> failwith ("[get_var_frame] Expected name "^id^" to be variable or value")

let rec gen_expr syms bbdfns this (e,t) vs ts sr =
  (*
  print_endline ("Generating expression " ^ string_of_bound_expression_with_type syms.dfns (e,t));
  *)
  let ge e = gen_expr syms bbdfns this e vs ts sr in
  if length ts <> length vs then 
  failwith 
  (
    "[gen_expr} wrong number of args, expected vs = " ^ 
    si (length vs) ^ 
    ", got ts=" ^ 
    si (length ts)
  );
  let tsub t = tsubst vs ts t in
  let tn t = cpp_typename syms (tsub (lower t)) in
  let ge_arg a = 
    match a with
    | _,`BTYP_tuple [] -> ""
    | x -> ge x 
  in
  let id,parent,sr,entry = 
    try Hashtbl.find bbdfns this 
    with _ -> failwith ("[gen_expr] Can't find this = " ^ si this)
  in
  let our_display = get_display_list bbdfns this in
  let our_level = length our_display in
  match e with
  | `BEXPR_expr (s,_) -> s
  | `BEXPR_coercion (e,t) -> 
    (* generate a function style cast *)
    tn t ^ "(" ^ ge e ^ ")"
    
  | `BEXPR_get_n (n,e) -> 
    "(" ^ ge e ^ ").mem_" ^ si n
    
  | `BEXPR_match_case (n,((e',t') as e)) -> 
    if is_unitsum t' then
      "((" ^ ge e ^ ") == " ^ si (n-1) ^ ")"
    else
      "((" ^ ge e ^ ").variant == " ^ si (n-1) ^ ")"

  | `BEXPR_case_arg (n,e) ->
    (* NOTE that the index is not used ... *)
    let _,t = e in
    let t = 
      match unfold t with 
      | `BTYP_sum ls -> nth ls (n-1)
      | _ -> assert false
    in
      let t = tn t in
      (* really ugly brackets .. *)
      "(*("^t^"*)((" ^ ge e ^ ").data))"
     
  | `BEXPR_deref ((`BEXPR_ref index),`BTYP_pointer t) -> 
    ge (`BEXPR_name index,t)

  | `BEXPR_deref e -> 
    begin match e with (_,t) ->
    "*("^tn t^"*)(" ^ ge e ^ ".data)"
    end

  | `BEXPR_literal v ->
    let t = tn t in
    t ^ "(" ^ cstring_of_literal v ^ ")"
    
  | `BEXPR_case (v,t') ->
    begin match unfold t' with
    | `BTYP_sum ls ->
       let n = length ls in
       if v<1 or v>n
       then 
         failwith 
         (
           "Invalid case index " ^ si v ^
           " of " ^ si n ^ " cases"
         )
       else let t' = nth ls (v-1) in
       if t' = `BTYP_tuple []
       then (* closure of const ctor is just the const value ???? *)
         if is_unitsum t then
           si (v-1)
         else
           "_uctor_(" ^ si (v-1) ^ ",0)"
       else 
         failwith 
         ( 
            "Can't handle closure of case " ^ 
            si v ^ 
            " of " ^
            string_of_btypecode syms.dfns t
         )
       (* "(" ^ tn (lower t) ^ "*)_uctor_" *)

    | _ -> failwith "Case tag must have sum type"
    end 
   
  | `BEXPR_name (index,ts') -> 
    let id,parent,sr2,entry = 
      try Hashtbl.find bbdfns index 
      with _ -> 
        match Hashtbl.find syms.dfns index with
        {id=id; sr=sr} -> syserr sr
        ("[gen_expr(name)] Can't find "^ id ^ "<" ^ si index ^ ">")
    in
    let ts = map tsub ts' in
    begin match entry with
      | `BBDCL_var (_,t)
      | `BBDCL_val (_,t) ->
          get_var_ref syms bbdfns this index ts

      | `BBDCL_const (_,_,ct,_) -> 
        let ts = map tn ts in
        csubst sr sr2 ct [] [] "Error" "Error" ts

      | `BBDCL_cstruct _ 
      | `BBDCL_struct _ 
      | `BBDCL_function _
      | `BBDCL_procedure _ 
      | `BBDCL_fun _
      | `BBDCL_proc _ ->
         failwith 
         (
           "[gen_expr: name] Open function '" ^ 
           id ^ "'<"^si index^
           "> in expression (closure required)"
         )
      | _ -> 
        failwith 
        (
          "[gen_expr: name] Cannot use this kind of name '"^
          id^"' in expression"
        )
    end

  | `BEXPR_closure (index,ts') -> 
    let id,parent,sr,entry = 
      try Hashtbl.find bbdfns index 
      with _ -> failwith ("[gen_expr(name)] Can't find index " ^ si index)
    in
    (*
    Should not be needed now .. 
    let ts = adjust_ts syms index ts' in
    *)
    let ts = map tsub ts' in
    begin match entry with
    | `BBDCL_function _ 
    | `BBDCL_procedure _ ->
      let the_display = 
        let d' = 
          map (fun (i,vslen) -> "ptr"^ cpp_instance_name syms bbdfns i (list_prefix ts vslen))
          (get_display_list bbdfns index)
        in 
          if length d' > our_level
          then "this" :: tl d'
          else d'
      in
      let name = cpp_instance_name syms bbdfns index ts in
      "(FLX_NEWP("^name^")"^
      "(" ^ cat ", " ("FLX_FPAR_PASS" :: the_display) ^
      "))"

    | `BBDCL_cstruct _ 
    | `BBDCL_struct _ 
    | `BBDCL_fun _
    | `BBDCL_proc _ ->
      failwith ("[gen_expr: closure] Can't wrap primitive proc, fun, or struct '"^id^"' yet")
    | _ -> failwith "[gen_expr: closure] Cannot use this kind of name '"^id^"' in expression"
    end

  | `BEXPR_ref (index,ts') -> 
    let ts = map tsub ts' in
    let t = lower t in
    let ref_type = tn (lower t) in
    let var_ptr = "&(" ^ get_var_ref syms bbdfns this index ts ^ ")" in
    let frame_ptr = get_var_frame syms bbdfns this index ts in
    let reference = ref_type ^ 
      "(" ^ frame_ptr ^ ", " ^ var_ptr ^ ")" 
    in
    reference

  | `BEXPR_apply 
     (
       (`BEXPR_case (v,t),t'),
       (a,t'') 
     ) ->
       (* t is the type of the sum, 
          t' is the function type of the constructor,
          t'' is the type of the argument
       *)
       let 
         arg_typename = tn (lower t'') 
       and
         union_typename = tn (lower t)
       in

       let aval = 
         "new (gc, "^arg_typename^"_ptr_map) " ^
         arg_typename ^ "(" ^ ge (a,t'') ^ ")"
       in
       let uval = "_uctor_(" ^ si (v-1) ^ ", " ^ aval ^")" in
       let s = "(" ^ union_typename ^ ")" ^ uval in
       s

       (*
       failwith 
       (
         "Trapped application, case " ^ 
         si v ^
         " of " ^ string_of_btypecode syms.dfns t ^
         "\ntype " ^ string_of_btypecode syms.dfns t' ^
         "\nargument=" ^ 
         string_of_bound_expression syms.dfns (a,t'') ^
         "\ntype " ^ string_of_btypecode syms.dfns t''
       )
      *) 


  | `BEXPR_apply_prim (index,ts,a) ->
    let id,parent,sr2,entry = 
      try Hashtbl.find bbdfns index 
      with _ -> failwith ("[gen_expr(apply instance)] Can't find index " ^ si index)
    in
    begin 
    match entry with
    | `BBDCL_fun (vs,ps,retyp,ct,_) -> 
      if length vs <> length ts then
      failwith 
      (
        "[get_expr:apply closure of fun] function " ^ 
        id ^ "<" ^ si index ^">" ^
        ", wrong number of args, expected vs = " ^ 
        si (length vs) ^ 
        ", got ts=" ^ 
        si (length ts)
      );
      let ts = map tsub ts in
      let retyp = tsubst vs ts retyp in
      let retyp = tn retyp in
      let ts = map tn ts in
      begin
        (*
        print_endline "--- CASE OF PRIMITIVE"; 
        *) 
        match a with
        | (`BEXPR_tuple es,`BTYP_tuple typs) ->
          let ess = map ge es in
          let ets = map tn typs in
          let tt = tn (`BTYP_tuple typs) in
          "(" ^ csubst sr sr2 ct ess ets tt retyp ts ^")"

        (* this case is needed for a primitive value of tuple type *)
        | (_,`BTYP_tuple typs) as x ->
          let n = length typs in
          let es = 
            map2 
            (fun i t -> `BEXPR_get_n (i,x),t) 
            (nlist n) typs
          in
          let ess = map ge es in
          let ets = map tn typs in
          let tt = tn (`BTYP_tuple typs) in
          "(" ^ csubst sr sr2 ct ess ets tt retyp ts ^")"

        | (_,typ) -> 
          let typ = tn typ in
          let a = ge a in
          "(" ^ csubst sr sr2 ct [a] [typ] typ retyp ts ^ ")"
      end

    (* not sure whether these will be prims or directs or what .. *)
    | `BBDCL_cstruct (vs,_) ->
     "(*("^ id ^"*)(void*)&(" ^ ge a ^ "))"
       
    (* not sure whether these will be prims or directs or what .. *)
    | `BBDCL_struct (vs,_) ->
      let name = tn (`BTYP_inst (index,ts)) in
      name ^ "(" ^ ge a ^ ")"

    (* but can't be a Felix function *)
    | _ -> 
      failwith 
      (
        "[gen_expr: apply prim] Expected '"^id^"' to be primitive function instance, got:\n" ^
        string_of_bbdcl syms.dfns entry index 
      )
    end

  | `BEXPR_apply_direct (index,ts,a) ->
    let id,parent,sr2,entry = 
      try Hashtbl.find bbdfns index 
      with _ -> failwith ("[gen_expr(apply instance)] Can't find index " ^ si index)
    in
    begin 
    (*
    print_endline ("apply closure of "^ id );
    print_endline ("  .. argument is " ^ string_of_bound_expression syms.dfns a);
    *)
    match entry with
    | `BBDCL_function (props,vs,ps,retyp,_) ->
      let ts = map tsub ts in
      let the_display = 
        let d' = 
          map (fun (i,vslen)-> "ptr"^ cpp_instance_name syms bbdfns i (list_prefix ts vslen))
          (get_display_list bbdfns index)
        in 
          if length d' > our_level
          then "this" :: tl d'
          else d'
      in
      let name = cpp_instance_name syms bbdfns index ts in
      "(FLX_NEWP("^name^")"^
      "(" ^ cat ", " ("FLX_FPAR_PASS" :: the_display) ^
      "))" ^
      "\n      ->apply(" ^ ge_arg a ^ ")"

    | `BBDCL_cstruct (vs,_) ->
     "(*("^ id ^"*)(void*)&(" ^ ge a ^ "))"
       
    | `BBDCL_struct (vs,_) ->
      let name = tn (`BTYP_inst (index,ts)) in
      name ^ "(" ^ ge a ^ ")"

    | _ -> 
      failwith 
      (
        "[gen_expr: apply_direct] Expected '"^id^"' to be generic function instance, got:\n" ^
        string_of_bbdcl syms.dfns entry index 
      )
    end

  | `BEXPR_apply_stack (index,ts,a) ->
    let id,parent,sr2,entry = 
      try Hashtbl.find bbdfns index 
      with _ -> failwith ("[gen_expr(apply instance)] Can't find index " ^ si index)
    in
    begin 
    (*
    print_endline ("apply closure of "^ id );
    print_endline ("  .. argument is " ^ string_of_bound_expression syms.dfns a);
    *)
    match entry with
    | `BBDCL_function (props,vs,ps,retyp,_) ->
      let ts = map tsub ts in
      let the_display = 
        let d' = 
          map (fun (i,vslen)-> "ptr"^ cpp_instance_name syms bbdfns i (list_prefix ts vslen))
          (get_display_list bbdfns index)
        in 
          if length d' > our_level
          then "this" :: tl d'
          else d'
      in
      let name = cpp_instance_name syms bbdfns index ts in
      "("^name^
      "(" ^ cat ", " ("FLX_FPAR_PASS" :: the_display) ^
      "))" ^
      "\n      .apply(" ^ ge_arg a ^ ")"

    | `BBDCL_cstruct (vs,_) ->
     "(*("^ id ^"*)(void*)&(" ^ ge a ^ "))"
       
    | `BBDCL_struct (vs,_) ->
      let name = tn (`BTYP_inst (index,ts)) in
      name ^ "(" ^ ge a ^ ")"

    | _ -> 
      failwith 
      (
        "[gen_expr: apply_direct] Expected '"^id^"' to be generic function instance, got:\n" ^
        string_of_bbdcl syms.dfns entry index 
      )
    end

  | `BEXPR_apply ((`BEXPR_closure (index,ts),_),a) ->
    assert false (* should have been factored out *)

  (* General application*)
  | `BEXPR_apply (f,a) ->
    (ge f) ^ "->clone()\n      ->apply(" ^ ge_arg a ^ ")"
    
  | `BEXPR_tuple es ->
    (* just apply the tuple type ctor to the arguments *)
    let ctyp = tn (lower t) in
    ctyp ^ "(" ^
    cat ", " (map ge es) ^
    ")"

type kind_t = Function | Procedure
 
let gen_prim_call 
  syms bbdfns tn ge ct ts a sr sr2
=
  let ts = map tn ts in
  match a with
  | (`BEXPR_tuple es,`BTYP_tuple typs) ->
    let ess = map (ge sr) es in
    let ets = map tn typs in
    let tt = tn (`BTYP_tuple typs) in
    "      {" ^ csubst sr sr2 ct ess ets tt "Error" ts^ "}\n"
  
  (* this case is needed for a primitive value of tuple type *)
  | (_,`BTYP_tuple typs) as x ->
    let n = length typs in
    let es = 
      map2 
      (fun i t -> `BEXPR_get_n (i,x),t) 
      (nlist n) typs
    in
    let ess = map (ge sr) es in
    let ets = map tn typs in
    let tt = tn (`BTYP_tuple typs) in
    "      {" ^ csubst sr sr2 ct ess ets tt "Error" ts^ "}\n"

  | (_,typ) -> 
    let typ = tn typ in
    let a = ge sr a in
    "      {" ^ csubst sr sr2 ct [a] [typ] typ "Error" ts^ "}\n"

let gen_exe syms (child_map,bbdfns) label_map counter this vs ts instance_no (exe:bexe_t) : string =
  if length ts <> length vs then 
  failwith 
  (
    "[gen_exe} wrong number of args, expected vs = " ^ 
    si (length vs) ^ 
    ", got ts=" ^ 
    si (length ts)
  );
  let sr = ("dummy",0,0,0,0) in
  let src_str = string_of_bexe syms.dfns 0 exe in
  (*
  print_endline ("generating exe " ^ string_of_bexe syms.dfns 0 exe);
  print_endline ("vs = " ^ catmap "," (fun (s,i) -> s ^ "->" ^ si i) vs);
  print_endline ("ts = " ^ catmap ","  (string_of_btypecode syms.dfns) ts);
  *)
  let tsub t = tsubst vs ts t in
  let ge sr e : string = gen_expr syms bbdfns this e vs ts sr in
  let tn t : string = cpp_typename syms (tsubst vs ts (lower t)) in
  let id,parent,sr,entry = 
    try Hashtbl.find bbdfns this 
    with _ -> failwith ("[gen_exe] Can't find this " ^ si this)
  in
  let our_display = get_display_list bbdfns this in
  let kind = match entry with
    | `BBDCL_function (_,_,_,_,_) -> Function
    | `BBDCL_procedure (_,_,_,_) -> Procedure
    | _ -> failwith "Expected executable code to be in function or procedure"
  in let our_level = length our_display in

  let handle_closure sr is_jump index ts subs a =
    let subs = 
      catmap ""
      (fun ((_,t) as e,s) ->
        let t = cpp_typename syms t in
        let e = ge sr e in
        "      " ^ t ^ " " ^ s ^ " = " ^ e ^ ";\n"
      )
      subs
    in
    let sub_start =
      if String.length subs = 0 then ""
      else "      {\n" ^ subs
    and sub_end =
      if String.length subs = 0 then ""
      else "      }\n"
    in
    let id,parent,sr2,entry = 
      try Hashtbl.find bbdfns index 
      with _ -> failwith ("[gen_exe(call)] Can't find index " ^ si index)
    in
    begin 
    match entry with
    | `BBDCL_proc (vs,_,ct,_) -> 
      assert (not is_jump);

      if length vs <> length ts then
      clierr sr "[gen_prim_call] Wrong number of type arguments"
      ;

      "      // " ^ src_str ^ "\n" ^
      sub_start ^
      gen_prim_call syms bbdfns tn ge ct ts a sr sr2 ^
      sub_end


    | `BBDCL_procedure (props,vs,ps,bexes) ->
      if bexes = []
      then 
      "      //call to empty procedure " ^ id ^ " elided\n"
      else begin
        let n = !counter in
        incr counter;
        let the_display = 
          let d' = 
            map (fun (i,vslen) -> "ptr"^cpp_instance_name syms bbdfns i (list_prefix ts vslen))
            (get_display_list bbdfns index)
          in 
            if length d' > our_level
            then "this" :: tl d'
            else d'
        in
        (* if we're calling from inside a function,
           we pass a 0 continuation as the caller 'return address'
           otherwise pass 'this' as the caller 'return address'
        *)
        let this = match kind with
          | Function -> 
            if is_jump 
            then 
              clierr sr "can't jump inside function" 
            else "0"
          | Procedure -> 
            if is_jump then "tmp"
            else "this"
        in

        let args = match a with
          | _,`BTYP_tuple [] -> this
          | _ -> this ^ ", " ^ ge sr a
        in

        let name = cpp_instance_name syms bbdfns index ts in
        let ptrmap = name ^ "_ptr_map" in
        begin 
          match kind with
          | Function ->
            "      //run procedure " ^ src_str ^ "\n" ^
            "      {\n" ^
            subs ^
            "      con_t *_p =\n" ^
            "      (FLX_NEWP(" ^ name ^ ")"^
            "(" ^ cat ",\n      " ("FLX_FPAR_PASS":: the_display) ^ "))\n"^
            "      ->call(\n      " ^ args ^ "\n      );\n" ^
            "      while(_p) _p=_p->resume();\n" ^
            "      }\n"

          | Procedure ->
            let call_string =
              "      return (FLX_NEWP(" ^ name ^ 
              ")(" ^ cat ",\n      " ("FLX_FPAR_PASS":: the_display) ^
              "))\n      ->call(\n      " ^ args ^ "\n      );\n"
            in
            if is_jump
            then
              "      //jump to procedure " ^ src_str ^ "\n" ^
              "      {\n" ^
              subs ^
              "      con_t *tmp = _caller;\n" ^
              "      _caller = 0;\n" ^
              call_string ^
              "      }\n"
            else
              "      //call procedure " ^ src_str ^ "\n" ^
              sub_start ^
              "      FLX_SET_PC(" ^ si n ^ ")\n" ^
              call_string ^
              sub_end ^
              "    FLX_CASE_LABEL(" ^ si n ^ ")\n"
        end
      end

    | _ -> 
      failwith 
      (
        "[gen_exe] Expected '"^id^"' to be procedure constant, got " ^
        string_of_bbdcl syms.dfns entry index 
      )
    end
  in
  let rec gexe exe = 
    match exe with
    | `BEXE_code (_,s) -> s
    | `BEXE_comment (_,s) -> "\n/*" ^ s ^ "*/\n"
    | `BEXE_label (_,s) -> 
      let local_labels = 
        try Hashtbl.find label_map this 
        with _ -> failwith ("[gen_exe] Can't find label map of " ^ si this)
      in
      let label_index = 
        try Hashtbl.find local_labels s 
        with _ -> failwith ("[gen_exe] In " ^ id ^ ": Can't find label " ^ s)
      in
      (match kind with
        | Procedure ->
        "    FLX_LABEL(" ^ si label_index ^ ","^si instance_no ^","^s^")\n" 
        | Function ->
        (* functions only allow local labels *)
        "    " ^ s ^ ":;\n"
      )

    | `BEXE_goto (sr,s) -> 
      begin match find_label bbdfns label_map this s with
      | `Local _ -> "      goto " ^ s ^ ";\n"
      | `Nonlocal (pc,frame) ->
        (* WHAT THIS CODE DOES: we pop the call stack until
           we find the first ancestor containing the target label,
           set the pc there, and return its continuation to the
           driver; we know the address of this frame because
           it must be in this function's display.
        *)
        let target_instance = 
          try Hashtbl.find syms.instances (frame, ts) 
          with Not_found -> failwith "Woops, bugged code, wrong type arguments for instance?"
        in
        let frame_ptr = "ptr" ^ cpp_instance_name syms bbdfns frame ts in
        "      // non local goto " ^ s ^ "\n" ^
        "      {\n" ^
        "        con_t *tmp1 = this;\n" ^
        "        while(tmp1 && " ^ frame_ptr ^ "!= tmp1)\n" ^
        "        {\n" ^
        "          con_t *tmp2 = tmp1->_caller;\n" ^
        "          tmp1 -> _caller = 0;\n" ^
        "          tmp1 = tmp2;\n" ^
        "        }\n" ^
        "      }\n" ^
        "      " ^ frame_ptr ^ "->pc = FLX_FARTARGET("^si pc^","^si target_instance^","^s^");\n" ^
        "      return " ^ frame_ptr ^ ";\n"
      | `Unreachable -> 
        clierr sr ("Jump to unreachable label " ^ s)
      end

    (* conditional gotos are always local *)
    | `BEXE_ifgoto (sr,e,s) -> "      if(" ^ ge sr e ^ ")goto " ^ s ^ ";\n"
    | `BEXE_ifnotgoto (sr,e,s) ->"      if(!("^ge sr e^"))goto " ^ s ^ ";\n" 
    
    | `BEXE_jump (sr,((`BEXPR_closure (index,ts),_) as f),a) ->
      let subs,x = unravel syms bbdfns a in
      let ts = map tsub ts in
      handle_closure sr true index ts subs x
      
    | `BEXE_call_prim (sr,index,ts,a) 
    | `BEXE_call_direct (sr,index,ts,a) 
    | `BEXE_call_stack (sr,index,ts,a) 
    | `BEXE_call (sr,(`BEXPR_closure (index,ts),_),a) ->
      let subs,x = unravel syms bbdfns a in
      let ts = map tsub ts in
      handle_closure sr false index ts subs x

    | `BEXE_loop (sr,i,a) ->
      let ptr = 
        if i= this then "this"
        else "ptr"^cpp_instance_name syms bbdfns i ts
      in
        print_endline ("Looping to " ^ ptr);
        let args = ptr ^ "->" ^ 
          (match a with
          | _,`BTYP_tuple [] -> "_caller"
          | _ -> "_caller, " ^ ge sr a
          )
        in
        "      //"^ src_str ^ "\n" ^
        (
          if i <> this then
          "      {\n" ^
          "        con_t *res = " ^ ptr ^ "\n      ->call(" ^ args ^");\n" ^
          "        printf(\"unwinding from %p to %p\\n\",this,"^ptr^");\n" ^
          "        con_t *p = this;\n" ^
          "        while(res && res != "^ptr^") { res = p->_caller; printf(\"called by %p\\n\",p); }\n"^
          "        for(con_t *tmp=this; tmp != (con_t*)"^ptr^";){//unwind stack\n" ^
          "           con_t *tmp2 = tmp->_caller;\n" ^
          "           printf(\"unwinding %p, caller is %p\\n\",tmp,tmp2);\n" ^
          "           tmp->_caller = 0;\n" ^
          "           tmp = tmp2;\n"^
          "        }\n" ^
          "        return res;\n" ^
          "      }\n"
          else 
          "      return " ^ ptr ^ "\n      ->call(" ^ args ^");\n"
        )

    (* If p is a variable containing a closure,
       and p recursively invokes the same closure,
       then the program counter and other state 
       of the closure would be lost, so we clone it
       instead .. the closure variables is never
       used (a waste if it isn't re-entered .. oh well)
     *)

    | `BEXE_call (sr,p,a) ->
      let args = 
        let this = match kind with
          | Procedure -> "this"
          | Function -> "0"
        in
        match a with
        | _,`BTYP_tuple [] -> this
        | _ -> this ^ ", " ^ ge sr a
      in
      begin match kind with
      | Function ->
        "      //run procedure " ^ src_str ^ "\n" ^
        "      {\n" ^
        "        con_t *_p = "^ge sr p ^ "->clone()\n      ->call("^args^");\n" ^
        "        while(_p) _p=_p->resume();\n" ^
        "      }\n"
        


      | Procedure ->
        let n = !counter in
        incr counter;
        "      //"^ src_str ^ "\n" ^
        "      FLX_SET_PC(" ^ si n ^ ")\n" ^
        "      return " ^ ge sr p ^ "->clone()\n      ->call(" ^ args ^");\n" ^
        "    FLX_CASE_LABEL(" ^ si n ^ ")\n"
      end

    | `BEXE_jump (sr,p,a) ->
      let args = match a with
        | _,`BTYP_tuple [] -> "tmp"
        | _ -> "tmp, " ^ ge sr a
      in
      "      //"^ src_str ^ "\n" ^
      "      {\n" ^
      "        con_t *tmp = _caller;\n" ^
      "        _caller=0;\n" ^
      "        return " ^ ge sr p ^ "\n      ->call(" ^ args ^");\n" ^
      "      }\n"

    | `BEXE_proc_return _ -> "      FLX_RETURN\n"

    | `BEXE_read (sr,index) -> 
      let id,parent,sr,entry = 
        try Hashtbl.find bbdfns index 
        with _ -> failwith ("[gen_expr(name)] Can't find index " ^ si index)
      in
      let t =
        match entry with
        | `BBDCL_var (_,t) -> t
        | `BBDCL_val (_,t) -> t
        | _ -> syserr sr "Expected read argument to be variable"
      in
      let n = !counter in
      incr counter;
      "      //read variable\n" ^
      "      p_message = &(" ^ get_var_ref syms bbdfns this index ts^");\n" ^
      "      action = wait_action;\n" ^
      "      FLX_SET_PC(" ^ si n ^ ")\n" ^
      "      return this;\n" ^
      "    FLX_CASE_LABEL(" ^ si n ^ ")\n"

      
    | `BEXE_regmatch (sr,e,reargs) ->
      let b = Buffer.create 500 in
      Flx_regen.regen b reargs (`regmatch e) (ge sr);
      Buffer.contents b

    | `BEXE_reglex (sr,p1,p2,reargs) ->
      let b = Buffer.create 500 in
      Flx_regen.regen b reargs (`reglex (p1,p2)) (ge sr);
      Buffer.contents b
      
    | `BEXE_fun_return (sr,e) -> 
      let _,t = e in
      let t = tn t in
      "      //" ^ src_str ^ "\n" ^
      "      {\n" ^
      "        "^t^" tmp="^ge sr e^";\n" ^
      "        return tmp;\n" ^
      "      }\n"

    | `BEXE_nop (_,s) -> "      //Nop: " ^ s ^ "\n"

    | `BEXE_assign (sr,e1,e2) ->
      "      //"^src_str^"\n" ^
      "      "^ ge sr e1 ^ " = " ^ ge sr e2 ^
      ";\n"
      
    | `BEXE_init (sr,v,e) -> 
      (*
      print_endline ("INIT of " ^ si v ^ " inside " ^ si this);
      *)
      "      //"^src_str^"\n" ^
      "      "^
      get_var_ref syms bbdfns this v ts^ 
      " = " ^ 
      ge sr e ^
      ";\n"
  in gexe exe

let gen_exes syms bbdfns display label_map counter index exes vs ts instance_no =
  cat ""
  (map (gen_exe syms bbdfns label_map counter index vs ts instance_no) exes)

(* PROCEDURES are implemented by continuations.
   The constructor accepts the display vector to
   form the closure object. The call method accepts
   the callers continuation object as a return address,
   and the procedure argument, and returns a continuation.
   The resume method runs the continuation until
   it returns a continuation to some object, possibly
   the same object. A flag in the continuation object
   determines whether the yield of control is a request
   for data or not (if so, the dispatcher must place the data
   in the nominated place before calling the resume method again.
*)
 
(* FUNCTIONS are implemented as functoids:
  the constructor accepts the display vector so as
  to form a closure object, the apply method
  accepts the argument and runs the function.
  The machine stack is used for functions.
*)
let gen_function_methods syms (child_map,bbdfns) label_map counter index ts instance_no = 
  let id,parent,sr,entry = Hashtbl.find bbdfns index in
  if syms.compiler_options.print_flag then
  print_endline 
  (
    "//Generating function body inst " ^ 
    si instance_no ^ "=" ^ 
    id ^ "<" ^si index^">" ^
    (
      if length ts = 0 then "" 
      else "[" ^ catmap "," (string_of_btypecode syms.dfns) ts ^ "]"
    )
  );
  match entry with 
  | `BBDCL_function (props,vs,bps,ret',exes) ->
    if length ts <> length vs then 
    failwith 
    (
      "[get_function_methods} wrong number of args, expected vs = " ^ 
      si (length vs) ^ 
      ", got ts=" ^ 
      si (length ts)
    );
    let argtype = lower (typeof_bparams bps) in
    let argtype = tsubst vs ts argtype in
    let ret = tsubst vs ts (lower ret') in
    let funtype = fold(`BTYP_function (argtype, ret)) in

    let argtypename = cpp_typename syms argtype in
    let name = cpp_instance_name syms bbdfns index ts in

    let display = get_display_list bbdfns index in

    let rettypename = cpp_typename syms ret in

    let ctor = 
      let vars =  find_references syms (child_map,bbdfns) index ts in
      let funs = filter 
        (fun (i,t) -> match t with 
        | `BTYP_function _ -> true 
        | _ -> false
        ) vars 
      in
      gen_ctor syms bbdfns name display funs ts 
    in
    let dtor = gen_dtor syms bbdfns name display ts in
    let params = map (fun (id,(ix,t)) -> ix) bps in
    let exe_string =
      try 
        gen_exes syms (child_map,bbdfns) display label_map counter index exes vs ts instance_no
      with x -> 
        print_endline (Printexc.to_string x);
        print_endline (catmap "\n" (string_of_bexe syms.dfns 1) exes);
        failwith "Can't gen exes .."
    in
    let cont = "con_t *" in
    let apply =
      rettypename^ " " ^name^
      "::apply("^
      (if argtype = `BTYP_tuple []
      then ""
      else argtypename ^" const &_arg ")^
      "){\n" ^
      "  collector_t &gc = *ptf->gc;\n" ^
      (
        match length params with
        | 0 -> ""
        | 1 -> 
          let i = hd params in
          if Hashtbl.mem syms.instances (i, ts)
          && not (argtype = `BTYP_tuple [])
          then
            "  " ^ cpp_instance_name syms bbdfns i ts ^ " = _arg;\n"
          else ""
        | _ -> 
          let counter = ref 0 in fold_left 
          (fun s i -> 
            let n = !counter in incr counter; 
            if Hashtbl.mem syms.instances (i,ts)
            then 
              s ^ "  " ^ cpp_instance_name syms bbdfns i ts ^ " = _arg.mem_"^ si n ^";\n"
            else s (* elide initialisation of elided variable *)
          )
          "" params 
      )^
      exe_string ^
      "}\n"
    and clone =
      "  " ^ name ^ "* "^name^"::clone()const {\n"^
      "  return new(*ptf->gc,"^name^"_ptr_map) "^name^"(*this);\n"^
      "}\n"
    in
      let q = qualified_name_of_index syms.dfns index in
      "//FUNC " ^ q ^ ": Constructor\n" ^
      ctor^ "\n" ^
       "//FUNC " ^ q ^ ": Destructor\n" ^
      dtor^ "\n" ^
      "\n//FUNC " ^ q ^ ": Clone method\n" ^
      clone^ "\n" ^
      "//FUNC " ^ q ^ ": Apply method\n" ^
      apply^ "\n"

  | _ -> failwith "function expected"

let gen_procedure_methods syms (child_map,bbdfns) label_map counter index ts instance_no = 
  let id,parent,sr,entry = Hashtbl.find bbdfns index in (* can't fail *)
  if syms.compiler_options.print_flag then
  print_endline 
  (
    "//Generating procedure body inst " ^ 
    si instance_no ^ "=" ^ 
    id ^ "<" ^si index^">" ^
    (
      if length ts = 0 then "" 
      else "[" ^ catmap "," (string_of_btypecode syms.dfns) ts ^ "]"
    )
  );
  match entry with 
  | `BBDCL_procedure (props,vs,bps,exes) ->
    if length ts <> length vs then 
    failwith 
    (
      "[get_procedure_methods} wrong number of args, expected vs = " ^ 
      si (length vs) ^ 
      ", got ts=" ^ 
      si (length ts)
    );
    let argtype = lower (typeof_bparams bps) in
    let argtype = tsubst vs ts argtype in
    let funtype = fold(`BTYP_function (argtype, `BTYP_void)) in

    let argtypename = cpp_typename syms argtype in
    let name = cpp_instance_name syms bbdfns index ts in

    let display = get_display_list bbdfns index in

    let ctor = 
      let vars =  find_references syms (child_map,bbdfns) index ts in
      let funs = filter 
        (fun (i,t) -> match t with 
        | `BTYP_function _ -> true 
        | _ -> false
        ) vars 
      in
      gen_ctor syms bbdfns name display funs ts 
    in

    let dtor = gen_dtor syms bbdfns name display ts in
    let ps = map (fun (id,(ix,t)) -> id,t) bps in
    let params = map (fun (id,(ix,t)) -> ix) bps in

    let exe_string =
      gen_exes syms (child_map,bbdfns) display label_map counter index exes vs ts instance_no
    in
    let cont = "con_t *" in
    let arg_sig = 
      match argtype with
      | `BTYP_tuple [] ->
      "  " ^ cont ^ "_ptr_caller"
      | _ ->
      "  " ^ cont ^ "_ptr_caller,\n"^
      "  " ^ argtypename ^" const &_arg\n"
    in
    let call =
      cont ^name^
      "::call(\n" ^ arg_sig ^
      "){\n" ^
      (
        "  _caller = _ptr_caller;\n" ^
        (match length bps with
        | 0 -> ""
        | 1 -> 
          let _,(i,_) = hd bps in
          if Hashtbl.mem syms.instances (i,ts)
          && not (argtype = `BTYP_tuple[])
          then
            "  " ^ cpp_instance_name syms bbdfns i ts ^ " = _arg;\n"
          else ""

        | _ -> let counter = ref 0 in fold_left 
          (fun s i -> 
            let n = !counter in incr counter; 
            if Hashtbl.mem syms.instances (i,ts)
            then
              s ^ "  " ^ cpp_instance_name syms bbdfns i ts ^ " = _arg.mem_"^ si n ^";\n"
            else s (* elide initialisation of elided variables *)
          )
          "" params
          ) ^
          "  INIT_PC\n" ^
          "  return this;\n"
      )^
      "}\n"
    and resume = 
      if exes = []
      then
        cont^name^"::resume(){//empty\n"^
        "     FLX_RETURN\n" ^ 
        "}\n"
      else
        cont^name^"::resume(){\n"^
        "  collector_t &gc = *ptf->gc;\n" ^
        "  FLX_START_SWITCH\n" ^
        exe_string ^
        "    FLX_RETURN\n" ^ (* HACK .. should be in exe_string .. *)
        "  FLX_END_SWITCH\n" ^
        "}\n" 
    and clone =
      "  " ^name^"* "^name^"::clone()const {\n" ^
        "  return new(*ptf->gc,"^name^"_ptr_map) "^name^"(*this);\n" ^
        "}\n"
    in
      let q = 
        try qualified_name_of_index syms.dfns index 
        with Not_found -> 
          si instance_no ^ "=" ^ 
          id ^ "<" ^si index^">" ^
          (
            if length ts = 0 then "" 
            else "[" ^ catmap "," (string_of_btypecode syms.dfns) ts ^ "]"
          )
      in
      "\n//PROC " ^ q ^ "\n" ^
      "//PROC " ^ q ^ ": Constructor\n" ^
      ctor^
      "\n//PROC " ^ q ^ ": Destructor\n" ^
      dtor^
      "\n//PROC " ^ q ^ ": Clone method\n" ^
      clone^
      "\n//PROC " ^ q ^ ": Call method\n" ^
      call^
      "\n//PROC " ^ q ^ ": Resume method\n" ^
      resume

  | _ -> failwith "procedure expected"


let gen_execute_methods syms (child_map,bbdfns) label_map counter bf =
  let s = Buffer.create 2000 in
  Hashtbl.iter
  (fun (index,ts) instance_no -> 
  let id,parent,sr,entry = Hashtbl.find bbdfns index in 
  begin match entry with
  | `BBDCL_function (props,vs,ps, ret, _) -> 
    bcat s ("//------------------------------\n");
    bcat s (
      gen_function_methods syms (child_map,bbdfns) label_map counter index ts instance_no
    )
    
    
  | `BBDCL_procedure (props,vs,ps,_) -> 
    bcat s ("//------------------------------\n");
    bcat s (
      gen_procedure_methods syms (child_map,bbdfns) label_map counter index ts instance_no
    )
  | _ -> ()
  end
  ;
  output_string bf (Buffer.contents s);
  Buffer.clear s
  )
  syms.instances

let gen_biface_header syms bbdfns biface = match biface with
  | `BIFACE_export_fun (sr,index, export_name) ->
    let id,parent,sr,entry = Hashtbl.find bbdfns index in
    begin match entry with
    | `BBDCL_function (props,vs,ps, ret, _) ->
      let display = get_display_list bbdfns index in
      if length display <> 0
      then clierr sr "Can't export nested function";

      let argtypes = 
        map 
        (fun (_,(_,x)) -> cpp_typename syms x) 
        ps 
      in
      let arglist = "  " ^ cat ",\n  " ("FLX_FPAR_DECL":: argtypes) in
      let rettypename = cpp_typename syms ret in

      "//EXPORT FUNCTION " ^ cpp_instance_name syms bbdfns index [] ^ 
      " as " ^ export_name ^ "\n" ^
      "extern \"C\" " ^ rettypename ^" " ^ export_name ^ "(\n" ^ arglist ^ "\n);\n"

    | `BBDCL_procedure (props,vs,ps, _) ->
      let display = get_display_list bbdfns index in
      if length display <> 0
      then clierr sr "Can't export nested proc";

      let argtypes = 
        map 
        (fun (_,(_,x)) -> cpp_typename syms x) 
        ps 
      in
      let arglist = 
        "  " ^ cat ",\n  " ("FLX_FPAR_DECL"::argtypes) 
      in 
      
      "//EXPORT PROCEDURE " ^ cpp_instance_name syms bbdfns index [] ^ 
      " as " ^ export_name ^ "\n" ^
      "extern \"C\" con_t * "  ^ export_name ^ "(\n" ^ arglist ^ "\n);\n"
  
    | _ -> failwith "Not implemented: export non-function/procedure"
    end
  
  | `BIFACE_export_type (sr, typ, export_name) ->
    "//EXPORT type " ^ sbt  syms.dfns typ ^ " as " ^ export_name  ^ "\n" ^
    "typedef " ^ cpp_type_classname syms typ ^ " " ^ export_name ^ "_class;\n" ^
    "typedef " ^ cpp_typename syms typ ^ " " ^ export_name ^ ";\n"
      
let gen_biface_body syms bbdfns biface = match biface with
  | `BIFACE_export_fun (sr,index, export_name) ->
    let id,parent,sr,entry = Hashtbl.find bbdfns index in
    begin match entry with
    | `BBDCL_function (props,vs,ps, ret, _) ->
      if length vs <> 0
      then clierr sr ("Can't export generic function " ^ id)
      ;
      let display = get_display_list bbdfns index in
      if length display <> 0
      then clierr sr "Can't export nested function";

      let argtypes = 
        map 
        (fun (id,(ix,t)) -> cpp_typename syms t ^ " " ^ id) 
        ps 
      in
      let arglist = "  " ^ cat ",\n  " ("FLX_FPAR_DECL":: argtypes) in
      let rettypename = cpp_typename syms ret in
      let class_name = cpp_instance_name syms bbdfns index [] in

      "//EXPORT FUNCTION " ^ cpp_instance_name syms bbdfns index [] ^ 
      " as " ^ export_name ^ "\n" ^
      rettypename ^" " ^ export_name ^ "(\n" ^ arglist ^ "\n){\n" ^
      "  return (new(*_ptf->gc,"^class_name^"_ptr_map)\n" ^
      "    " ^ class_name ^ "(_pgf,_ppf,_ptf))\n" ^
      "    ->apply(" ^ cat ", " (map fst ps) ^ ");\n" ^
      "}\n"

    | `BBDCL_procedure (props,vs,ps,_) ->
      if length vs <> 0
      then clierr sr ("Can't export generic procedure " ^ id)
      ;
      let display = get_display_list bbdfns index in
      if length display <> 0
      then clierr sr "Can't export nested function";

      let argtypes = 
        map 
        (fun (id,(_,t)) -> cpp_typename syms t ^ " " ^ id) 
        ps 
      in
      let arglist = "  " ^ cat ",\n  " ("FLX_FPAR_DECL" ::argtypes) in
      let class_name = cpp_instance_name syms bbdfns index [] in

      "//EXPORT PROC " ^ cpp_instance_name syms bbdfns index [] ^ 
      " as " ^ export_name ^ "\n" ^
      "con_t *" ^ export_name ^ "(\n" ^ arglist ^ "\n){\n" ^
      "  return (new(*_ptf->gc,"^class_name^"_ptr_map)\n" ^
      "    " ^ class_name ^ "(_pgf,_ppf,_ptf))" ^
      "\n      ->call(" ^ cat ", " ("0"::(map fst ps)) ^ ");\n" ^
      "}\n"

    | _ -> failwith "Not implemented: export non-function/procedure"
    end

  | `BIFACE_export_type _ -> ""

let gen_biface_headers syms bbdfns bifaces = 
  cat "" (map (gen_biface_header syms bbdfns) bifaces)

let gen_biface_bodies syms bbdfns bifaces = 
  cat "" (map (gen_biface_body syms bbdfns) bifaces)

@h = tangler('src/flxg.ml')
@select(h)
open Flx_util
open Flx_ast
open Flx_types
open Flx_mtypes1
open Flx_print
open Flx_srcref
open Flx_desugar
open Flx_bbind
open Flx_name
open Flx_tgen
open Flx_gen
open Flx_symtab
open Flx_getopt
open Flx_version
open Flx_exceptions
open Flx_flxopt
open Flx_ogen
;;

let print_help () = print_options(); exit(0)
;;

let reverse_return_parity = ref false
;;

let last_time = ref 0.0
;;
let tim() = 
  let now = (Unix.times()).Unix.tms_utime in
  let elapsed = now -. !last_time in
  last_time := now;
  elapsed
;;

let format_time tm = 
  si (tm.Unix.tm_year + 1900) ^ "/" ^
  si (tm.Unix.tm_mon + 1) ^ "/" ^
  si tm.Unix.tm_mday ^ " " ^
  si tm.Unix.tm_hour ^ ":" ^
  si tm.Unix.tm_min ^ ":" ^
  si tm.Unix.tm_sec 
;;
try
  let compile_start = Unix.time () in
  let compile_start_gm = Unix.gmtime compile_start in
  let compile_start_local = Unix.localtime compile_start in
  let compile_start_gm_string = format_time compile_start_gm ^ " UTC" in
  let compile_start_local_string = format_time compile_start_local ^ " (local)" in
  let argc = Array.length Sys.argv in
  if argc <= 1 
  then begin
    print_endline "usage: flxg --key=value ... filename; -h for help";
    exit 0
  end
  ;
  let raw_options = parse_options Sys.argv in
  let compiler_options = get_felix_options raw_options in
  reverse_return_parity := compiler_options.reverse_return_parity
  ;
  let syms = make_syms compiler_options in
  if check_keys raw_options ["h"; "help"]
  then print_help ()
  ;
  if check_key raw_options "version" 
  then (print_endline ("Felix Version " ^ !version_data.version_string))
  ;
  if compiler_options.print_flag then begin
    print_string "//Include directories = ";
    List.iter (fun d -> print_string (d ^ " "))
    compiler_options.include_dirs;
    print_endline ""
  end
  ;

 let filename = 
    match get_key_value raw_options "" with
    | Some s -> s
    | None -> exit 0
  in
  let filebase = filename in
  let input_file_name = filebase ^ ".flx" 
  and iface_file_name = filebase ^ ".fix"
  and header_file_name = filebase ^ ".hpp"
  and body_file_name = filebase ^ ".cpp" 
  and report_file_name = filebase ^ ".xref" 
  and module_name = 
    let n = String.length filebase in 
    let i = ref (n-1) in
    while !i <> -1 && filebase.[!i] <> '/' do decr i done;
    String.sub filebase (!i+1) (n - !i - 1)
  in

  let include_dirs =  (Filename.dirname input_file_name) :: compiler_options.include_dirs in
  let compiler_options = { compiler_options with include_dirs = include_dirs } in
  let syms = { syms with compiler_options = compiler_options } in

  (* PARSE THE IMPLEMENTATION FILE *)

  if compiler_options.print_flag
  then print_endline ("//Parsing Implementation " ^ input_file_name);
  let parse_tree = 
    Flx_desugar.include_file syms input_file_name false
  in
  if compiler_options.print_flag
  then print_endline (Flx_print.string_of_compilation_unit parse_tree);

  let parse_time = tim() in
  if compiler_options.print_flag
  then print_endline ("//PARSE OK time " ^ string_of_float parse_time);

  if compiler_options.print_flag 
  then print_endline "//DESUGARING";

  let deblocked = 
    desugar_program syms module_name parse_tree 
  in
  let desugar_time = tim() in
  if compiler_options.print_flag 
  then print_endline ("//DESUGAR time " ^ string_of_float desugar_time);

  (* THIS IS A HACK! *)
  let root = !(syms.counter) in
  if compiler_options.print_flag 
  then print_endline ("//Top level module '"^module_name^"' has index " ^ si root);


  if compiler_options.print_flag 
  then print_endline "//BUILDING TABLES";

  let pubtab, _, exes, ifaces,dirs = 
    build_tables syms "<top level>" 0 None None root deblocked 
  in
  let build_table_time = tim() in
  if compiler_options.print_flag 
  then print_endline ("//BUILDING TABLES time " ^ string_of_float build_table_time);


  if compiler_options.print_flag 
  then print_endline "//BINDING EXECUTABLE CODE"
  ;
  let bbdfns = bbind syms in
  let child_map = cal_children syms bbdfns in
  let bifaces = bind_ifaces syms ifaces in
  Hashtbl.clear syms.ticache;

  let binding_time = tim() in

  if compiler_options.print_flag 
  then print_endline ("//Binding complete time " ^ string_of_float binding_time);

  if compiler_options.print_flag 
  then print_endline "//CHECKING ROOT";

  let root_proc = 
    match 
      try Hashtbl.find syms.dfns root 
      with Not_found ->
        failwith 
        (
          "Can't find root module " ^ si root ^
          " in symbol table?"
        )
    with {id=id; sr=sr; parent=parent;vs=vs;pubmap=name_map;symdef=entry} ->
    begin match entry with
      | `SYMDEF_module -> ()
      | _ -> failwith "Expected to find top level module ''"
    end
    ;
    let entry = 
      try Hashtbl.find name_map "_init_" 
      with Not_found ->
        failwith "Can't find name _init_ in top level module's name map"
    in
    let index = match entry with
      | FunctionEntry [x] -> x
      | FunctionEntry [] -> failwith "Couldn't find '_init_'"
      | FunctionEntry _ -> failwith "Too many top level procedures called '_init_'"
      | NonFunctionEntry _ -> failwith "_init_ found but not procedure"
    in 
    if compiler_options.print_flag 
    then print_endline ("//root module's init procedure has index " ^ si index);
    index
  in

  if compiler_options.print_flag 
  then print_endline "//OPTIMISING";
  let () = Flx_use.find_roots syms bbdfns root_proc bifaces in
  let bbdfns = Flx_useless.elide_unused syms (child_map,bbdfns) in
  let child_map = cal_children syms bbdfns in
  (*
  let bbdfns = Flx_useless.elide_unused syms (child_map,bbdfns) in
  let child_map = cal_children syms bbdfns in
  *)

  let bbdfns = ref bbdfns in
  let child_map = ref child_map in
  if compiler_options.heavy_inlining then
  begin
    if compiler_options.print_flag then
    print_endline "//INLINING";
    Flx_inline.heavy_inlining syms (!child_map,!bbdfns) root_proc bifaces;
    child_map := cal_children syms !bbdfns;
    bbdfns := Flx_useless.elide_unused syms (!child_map,!bbdfns);
    child_map := cal_children syms !bbdfns
  end
  ;
  let bbdfns = !bbdfns in
  let child_map = !child_map in

  if compiler_options.print_flag 
  then print_endline "//Calculating stackable calls";
  let bbdfns = Flx_stack_calls.make_stack_calls syms (child_map,bbdfns) in


  if compiler_options.print_flag 
  then print_endline "//Generating primitive wrapper closures";
  Flx_mkcls.make_closures syms bbdfns; 
  let child_map = cal_children syms bbdfns in

  if compiler_options.print_flag then 
  begin
    let f = open_out report_file_name in
    Flx_call.print_call_report bbdfns f;
    close_out f
  end
  ;

  if compiler_options.print_flag 
  then print_endline "//instantiating";

  Flx_inst.instantiate syms bbdfns root_proc bifaces;
  let top_class =
    try cpp_instance_name syms bbdfns root_proc []
    with Not_found -> 
      failwith ("can't name instance of root _init_ procedure index " ^ si root_proc)
  in
  if compiler_options.print_flag 
  then print_endline ("//root module's init procedure has name " ^ top_class);
  let instantiation_time = tim() in

  if compiler_options.print_flag 
  then print_endline ("//instantiation time " ^ string_of_float instantiation_time);
 
  if compiler_options.compile_only
  then exit (if compiler_options.reverse_return_parity then 1 else 0)
  ;

  let hf = open_out header_file_name in
  let bf = open_out body_file_name in 
  let psh s = output_string hf s in
  let psb s = output_string bf s in
  let plh s = psh s; psh  "\n" in
  let plb s = psb s; psb "\n" in

  if compiler_options.print_flag 
  then print_endline "//GENERATING C++: user headers";
  
  plh ("#ifndef _FLX_GUARD_" ^ module_name);
  plh ("#define _FLX_GUARD_" ^ module_name);
  plh ("//Input file: " ^ input_file_name);
  plh ("//Generated by Felix Version " ^ !version_data.version_string);
  plh ("//Timestamp: " ^ compile_start_gm_string);
  plh ("//Timestamp: " ^ compile_start_local_string);
  plh "";
  plh "//FELIX RUNTIME";
  plh "#include \"rtl/flx_rtl.hpp\"";
  plh "using namespace flx::rtl;";
  plh "#include \"rtl/flx_collector.hpp\"";
  plh "using namespace flx::gc::generic;";
  plh "";

  plh "\n//-----------------------------------------";
  plh "//USER HEADERS";
  (* These must be in order: build a list and sort it *)
  let dfnlist = ref [] in
  Hashtbl.iter
  (fun index data -> dfnlist := index :: !dfnlist)
  bbdfns
  ;
  List.iter 
  (fun index ->
    match Hashtbl.find bbdfns index 
    with (id,parent,sr,entry) ->
    match entry with
    | `BBDCL_header (s,_) -> plh s
    | _ -> ()
  )
  (List.sort compare !dfnlist)
  ;

  plh "\n//-----------------------------------------";
  List.iter plh [
  "//FELIX SYSTEM";

  (* GLOBAL FRAME *)
  "struct global_frame_t {";
  " // put global values here";
  "};";
  "struct process_frame_t {";
  "  global_frame_t *global_frame;";
  "  process_frame_t(";
  "    global_frame_t *_global_frame";
  "  ) :";
  "    global_frame(_global_frame)";
  "  {}";
  "};";

  "struct thread_frame_t;"
  ]
  ;
  if compiler_options.print_flag then 
  print_endline "//GENERATING C++: collect types";
  let types = ref [] in 
    Hashtbl.iter 
    (fun t index-> types := (index, t) :: !types)
    syms.registry
  ;
  let types = 
    List.sort 
    (
      fun a1 a2 -> compare (fst a1) (fst a2)
    ) 
    !types
  in
  (*
  List.iter
  (fun (_,t) -> print_endline (string_of_btypecode dfns t))
  types
  ;
  *)

  if compiler_options.print_flag then 
  print_endline "//GENERATING C++: type class names";
  plh "\n//-----------------------------------------";
  plh "//NAME THE TYPES";
  plh  (gen_type_names syms bbdfns types);

  if compiler_options.print_flag then 
  print_endline "//GENERATING C++: type class definitions";
  plh "\n//-----------------------------------------";
  plh  "//DEFINE THE TYPES";
  plh  (gen_types syms bbdfns types);

  if compiler_options.print_flag then 
  print_endline "//GENERATING C++: function and procedure classes";
  plh "\n//-----------------------------------------";
  plh  "//DEFINE FUNCTION CLASSES";
  plh  (gen_functions syms (child_map,bbdfns));

  let topvars_with_type = find_thread_vars_with_type bbdfns in
  let topvars = List.map fst topvars_with_type in
  List.iter plh 
  [
  "struct thread_frame_t {";
  "  global_frame_t *global_frame;";
  "  process_frame_t *process_frame;";
  "  collector_t *gc;";
  "  thread_frame_t(";
  "    global_frame_t*,";
  "    process_frame_t*,";
  "    collector_t*";
  "  );";
  ]
  ;
  plh (format_vars syms bbdfns topvars []);
  plh "};";

  (* BODY *)
  if compiler_options.print_flag then 
  print_endline "//GENERATING C++: GC ptr maps & offsets";

  plb ("//Input file: " ^ input_file_name);
  plb ("//Generated by Felix Version " ^ !version_data.version_string);
  plb ("//Timestamp: " ^ compile_start_gm_string);
  plb ("//Timestamp: " ^ compile_start_local_string);

  plb ("#include \"" ^ module_name ^ ".hpp\"");
  plb "#include <stdio.h>"; (* for diagnostics *)
  plb "#define comma ,";
  plb "\n//-----------------------------------------";
  plb "//EMIT USER BODY CODE";
  (* These must be in order: build a list and sort it *)
  let dfnlist = ref [] in
  Hashtbl.iter
  (fun index data -> dfnlist := index :: !dfnlist)
  bbdfns
  ;
  List.iter 
  (fun index ->
    match Hashtbl.find bbdfns index 
    with (id,parent,sr,entry) ->
    match entry with
    | `BBDCL_body (s,_) -> plb s
    | _ -> ()
  )
  (List.sort compare !dfnlist)
  ;

  plb "\n//-----------------------------------------";
  plb "//Thread Frame Constructor";

  let topfuns = List.filter 
    (fun (i,t) -> match t with 
    | `BTYP_function _ -> true 
    | _ -> false
    ) topvars_with_type
  in
  let topfuns = List.map fst topfuns in
  let topinits =
    [
      "  process_frame(pf_a)";
      "  global_frame(gf_a)";
      "  gc(gc_a)"
    ]
    @
    List.map
    (fun index -> 
      "  " ^ 
      cpp_instance_name syms bbdfns index [] ^ 
      "(0)"
    )
    topfuns
  in
  let topinits = String.concat ",\n" topinits in
  List.iter plb
  [
  "thread_frame_t::thread_frame_t(";
  "  global_frame_t *gf_a,";
  "  process_frame_t *pf_a,";
  "  collector_t *gc_a";
  ") :";
  topinits;
  "{}"
  ];



  plb "\n//-----------------------------------------";
  plb "//DEFINE OFFSET tables for GC";
  plb "static std::size_t _uctor_offsets[1]= { offsetof(_uctor_,data) };";
  plb "static gc_shape_t _uctor_ptr_map ("; 
  plb "  1,";
  plb "  sizeof(_uctor_),";
  plb "  0,";
  plb "  1,";
  plb "  _uctor_offsets";
  plb ");";

  plb (Flx_ogen.gen_offset_tables syms (child_map,bbdfns));

  let label_map = Flx_label.create_label_map bbdfns syms.counter in

  plb "\n//-----------------------------------------";
  plb "#ifdef FLX_CGOTO";
  plb "//DEFINE LABELS for GNUC ASSEMBLER LABEL HACK";
  Hashtbl.iter
  (fun (fno,_) inst ->
    try 
      let labels = Hashtbl.find label_map fno in
      Hashtbl.iter
      (fun lab lno ->
        plb ("FLX_DECLARE_LABEL(" ^ si lno ^ ","^ si inst ^ "," ^ lab^")")
      )
      labels
    with Not_found -> ()
  )
  syms.instances
  ;
  plb "#endif";

  if compiler_options.print_flag then 
  print_endline "//GENERATING C++: method bodies";

  plb "\n//-----------------------------------------";
  plb "//DEFINE FUNCTION CLASS METHODS";
  gen_execute_methods syms (child_map,bbdfns) label_map syms.counter bf;

  if compiler_options.print_flag then print_endline "//GENERATING C++: interface";
  plb "\n//-----------------------------------------";
  plb "//CREATE STANDARD EXTERNAL INTERFACE";
  plb "extern \"C\" global_frame_t *create_global_frame() {";
  plb "  return new global_frame_t;";
  plb "}";

  plb "extern \"C\" void destroy_global_frame(";
  plb "  global_frame_t *pgf";
  plb ") {";
  plb "  delete pgf;";
  plb "}";

  plb "extern \"C\" process_frame_t *create_process_frame(";
  plb "  global_frame_t *pgf";
  plb  ") {";
  plb "  return new process_frame_t(pgf);";
  plb "}";

  plb "extern \"C\" void destroy_process_frame(";
  plb "  process_frame_t *ppf";
  plb  ") {";
  plb "  delete ppf;";
  plb "}";

  plb "extern \"C\" thread_frame_t *create_thread_frame(";
  plb "  global_frame_t *pgf,";
  plb "  process_frame_t *ppf,";
  plb "  collector_t *gc";
  plb ") {";
  plb "  thread_frame_t *p = new(*gc,thread_frame_t_ptr_map) thread_frame_t(pgf,ppf,gc);";
  plb "  gc->add_root(p);";
  plb "  return p;";
  plb "}";

  plb "extern \"C\" void destroy_thread_frame(";
  plb "  thread_frame_t *ptf";
  plb ") {";
  plb "  ptf->gc->remove_root(ptf);";
  plb "}";

  plb "extern \"C\" con_t *start(";
  plb "  global_frame_t *pgf,";
  plb "  process_frame_t *ppf,";
  plb "  thread_frame_t *ptf";
  plb ") {";
  plb ("  return (new(*ptf->gc,"^top_class^"_ptr_map) " ^ top_class ^ "(pgf,ppf,ptf))\n      ->call(0);");
  plb "}";

  plb "\n//-----------------------------------------";
  plh "//DECLARE USER EXPORTS";
  plh (gen_biface_headers syms bbdfns bifaces);
  plb "//DEFINE EXPORTS";
  plb (gen_biface_bodies syms bbdfns bifaces);

  plh "//header complete";
  plh "#endif";
  plb "//body complete";
  close_out hf;
  close_out bf;
  let code_generation_time = tim() in
  if compiler_options.print_flag then 
  print_endline ("//code generation time " ^ string_of_float code_generation_time);

  let total_time = 
    parse_time +.
    desugar_time +.
    build_table_time +.
    binding_time +.
    instantiation_time +.
    code_generation_time
  in  
  if compiler_options.print_flag then 
  print_endline ("//Felix compiler time " ^ string_of_float total_time);
  let fname = "flxg_stats.txt" in
  let 
    old_parse_time,
    old_desugar_time,
    old_build_table_time,
    old_binding_time,
    old_instantiation_time,
    old_code_generation_time,
    old_total_time
  = 
  let zeroes = 0.0,0.0,0.0,0.0,0.0,0.0,0.0 in
  let f = try Some (open_in fname) with _ -> None in
  begin match f with
  | None -> zeroes
  | Some f ->
    let x =
      try
        let id x1 x2 x3 x4 x5 x6 x7 = x1, x2, x3, x4, x5, x6, x7 in
        Scanf.fscanf f 
        "parse=%f desugar=%f build=%f bind=%f inst=%f gen=%f tot=%f"
        id
      with _ -> zeroes
    in close_in f; x
  end
  in
    let f = open_out fname in
    Printf.fprintf 
      f
      "parse=%f\ndesugar=%f\nbuild=%f\nbind=%f\ninst=%f\ngen=%f\ntot=%f\n"
      (old_parse_time +. parse_time)
      (old_desugar_time +. desugar_time)
      (old_build_table_time +. build_table_time)
      (old_binding_time +. binding_time)
      (old_instantiation_time +. instantiation_time)
      (old_code_generation_time +. code_generation_time)
      (old_total_time +. total_time)
    ;
    close_out f
  ;
  exit (if compiler_options.reverse_return_parity then 1 else 0)

with x -> Flx_terminate.terminate !reverse_return_parity x
;;

